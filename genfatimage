#!/usr/bin/perl
# SPDX-License-Identifier: BSD-2-Clause
# Copyright 2025 Intel Corporation - All Rights Reserved

use strict;
use integer;
use v5.16;
use utf8;
no indirect;

use Crypt::Random::Source;
use Date::Parse;
use Digest::CRC qw(crc32);
use Encode;
use Fcntl qw(:DEFAULT :mode);
use File::Spec;
use Digest::SHA;

die "$0 a 64-bit integer Perl interpreter is required\n" unless (1 << 63);

# Program name and version
my $myname    = 'genfatimage';
my $myversion = '0.0.1';
my $mycreator = 'GenFatIm';

# Options structure
my %opt = (
    # Defaults, including ones taken from the environment.
    'verbose' => $ENV{'V'},
    'volname' => $ENV{'VOLNAME'},
    'gptent' => 1,		# Minimum number of GPT entries
    'pad' => 1,			# Pad image by default
    'backup_gpt' => 1,		# Generate backup GPT
    'strict' => {}		# Strict flags, see below
);

# Strict flags
my %strict_help = (
    'volsize'   => 'Enforce filesystem type volume size limits',
    'upcase'    => 'Use Microsoft encoding for upcase table (exFAT)',
    'fats'      => 'Require two FATs (not exFAT)',
    'mbrspace'  => 'Add space after partition table (MBR)',
    'cluster'   => 'Max 32K clusters (not exFAT), min 4K (FAT32)',
    'bootspace' => 'Enforce 32-sector boot area space (FAT32)',
    'rootdir'   => 'Enforce minimum root directory size (FAT12/16)',
    'gptsize'   => 'Size GPT to 128 partition entries (GPT)',
    'creator'   => 'Set default system name to "WINNT4.1" (not exFAT)',
    'flags'     => 'Error out on unknown --strict flags',
    'filename'  => 'Error out on invalid filenames'
);

# Constants for partition types; the ordering matters for command-line parsing!
my $ANYPART = 0;
my $FLAT    = 1;
my $MBR     = 2;
my $GPT     = 3;

# The FAT and GPT Unicode encoding (UTF-16LE)
my $FATUNI = Encode::find_encoding('UTF-16LE');
# Convert a string to the "Unicode" as used by FAT and EFI
sub to_uni($) {
    my($s) = @_;
    return Encode::encode($FATUNI, $s, Encode::LEAVE_SRC);
}

# This is the Microsoft-specified "frozed" upcasing table for exFAT.
# Presumably it is also the one to use for FAT, which doesn't even
# *have* an on-disk upcase table. The upcase table encoding in the
# specification is suboptimal at 5836 bytes; recompressing it gives
# a table that is 2854 bytes long. Use the official encoding if
# $opt{strict} or with a cluster size exceeding 4K (in which case it
# doesn't really matter.)
sub upcase_table_expand(@) {
    my @t = ();
    for (my $cp = 0; $cp < 0x10000; $cp++) {
	my $tcp = shift(@_);
	if ($tcp == 0xffff && $cp < $tcp) {
	    my $n = shift(@_);
	    my $ecp = $cp+$n-1;
	    push(@t, $cp ... $ecp);
	    $cp = $ecp;
	} else {
	    push(@t, $tcp);
	}
    }
    die unless (scalar(@t) == 0x10000);
    return @t;
}
sub upcase_table_compress(@) {
    my @t = ();
    my $idem = 0;
    my $cp = 0;
    while (1) {
	my $tcp = shift(@_);
	if (defined($tcp) && $tcp == $cp) {
	    $idem++;
	} else {
	    if ($idem >= 3) {
		push(@t, 0xffff, $idem);
	    } else {
		push(@t, $cp-$idem ... $cp-1);
	    }
	    last unless (defined($tcp));
	    push(@t, $tcp);
	    $idem = 0;
	}
	$cp++;
    }
    die unless ($cp == 0x10000);
    return @t;
}


my @upcase_table_msft = (
    0x0000,0x0001,0x0002,0x0003,0x0004,0x0005,0x0006,0x0007,
    0x0008,0x0009,0x000a,0x000b,0x000c,0x000d,0x000e,0x000f,
    0x0010,0x0011,0x0012,0x0013,0x0014,0x0015,0x0016,0x0017,
    0x0018,0x0019,0x001a,0x001b,0x001c,0x001d,0x001e,0x001f,
    0x0020,0x0021,0x0022,0x0023,0x0024,0x0025,0x0026,0x0027,
    0x0028,0x0029,0x002a,0x002b,0x002c,0x002d,0x002e,0x002f,
    0x0030,0x0031,0x0032,0x0033,0x0034,0x0035,0x0036,0x0037,
    0x0038,0x0039,0x003a,0x003b,0x003c,0x003d,0x003e,0x003f,
    0x0040,0x0041,0x0042,0x0043,0x0044,0x0045,0x0046,0x0047,
    0x0048,0x0049,0x004a,0x004b,0x004c,0x004d,0x004e,0x004f,
    0x0050,0x0051,0x0052,0x0053,0x0054,0x0055,0x0056,0x0057,
    0x0058,0x0059,0x005a,0x005b,0x005c,0x005d,0x005e,0x005f,
    0x0060,0x0041,0x0042,0x0043,0x0044,0x0045,0x0046,0x0047,
    0x0048,0x0049,0x004a,0x004b,0x004c,0x004d,0x004e,0x004f,
    0x0050,0x0051,0x0052,0x0053,0x0054,0x0055,0x0056,0x0057,
    0x0058,0x0059,0x005a,0x007b,0x007c,0x007d,0x007e,0x007f,
    0x0080,0x0081,0x0082,0x0083,0x0084,0x0085,0x0086,0x0087,
    0x0088,0x0089,0x008a,0x008b,0x008c,0x008d,0x008e,0x008f,
    0x0090,0x0091,0x0092,0x0093,0x0094,0x0095,0x0096,0x0097,
    0x0098,0x0099,0x009a,0x009b,0x009c,0x009d,0x009e,0x009f,
    0x00a0,0x00a1,0x00a2,0x00a3,0x00a4,0x00a5,0x00a6,0x00a7,
    0x00a8,0x00a9,0x00aa,0x00ab,0x00ac,0x00ad,0x00ae,0x00af,
    0x00b0,0x00b1,0x00b2,0x00b3,0x00b4,0x00b5,0x00b6,0x00b7,
    0x00b8,0x00b9,0x00ba,0x00bb,0x00bc,0x00bd,0x00be,0x00bf,
    0x00c0,0x00c1,0x00c2,0x00c3,0x00c4,0x00c5,0x00c6,0x00c7,
    0x00c8,0x00c9,0x00ca,0x00cb,0x00cc,0x00cd,0x00ce,0x00cf,
    0x00d0,0x00d1,0x00d2,0x00d3,0x00d4,0x00d5,0x00d6,0x00d7,
    0x00d8,0x00d9,0x00da,0x00db,0x00dc,0x00dd,0x00de,0x00df,
    0x00c0,0x00c1,0x00c2,0x00c3,0x00c4,0x00c5,0x00c6,0x00c7,
    0x00c8,0x00c9,0x00ca,0x00cb,0x00cc,0x00cd,0x00ce,0x00cf,
    0x00d0,0x00d1,0x00d2,0x00d3,0x00d4,0x00d5,0x00d6,0x00f7,
    0x00d8,0x00d9,0x00da,0x00db,0x00dc,0x00dd,0x00de,0x0178,
    0x0100,0x0100,0x0102,0x0102,0x0104,0x0104,0x0106,0x0106,
    0x0108,0x0108,0x010a,0x010a,0x010c,0x010c,0x010e,0x010e,
    0x0110,0x0110,0x0112,0x0112,0x0114,0x0114,0x0116,0x0116,
    0x0118,0x0118,0x011a,0x011a,0x011c,0x011c,0x011e,0x011e,
    0x0120,0x0120,0x0122,0x0122,0x0124,0x0124,0x0126,0x0126,
    0x0128,0x0128,0x012a,0x012a,0x012c,0x012c,0x012e,0x012e,
    0x0130,0x0131,0x0132,0x0132,0x0134,0x0134,0x0136,0x0136,
    0x0138,0x0139,0x0139,0x013b,0x013b,0x013d,0x013d,0x013f,
    0x013f,0x0141,0x0141,0x0143,0x0143,0x0145,0x0145,0x0147,
    0x0147,0x0149,0x014a,0x014a,0x014c,0x014c,0x014e,0x014e,
    0x0150,0x0150,0x0152,0x0152,0x0154,0x0154,0x0156,0x0156,
    0x0158,0x0158,0x015a,0x015a,0x015c,0x015c,0x015e,0x015e,
    0x0160,0x0160,0x0162,0x0162,0x0164,0x0164,0x0166,0x0166,
    0x0168,0x0168,0x016a,0x016a,0x016c,0x016c,0x016e,0x016e,
    0x0170,0x0170,0x0172,0x0172,0x0174,0x0174,0x0176,0x0176,
    0x0178,0x0179,0x0179,0x017b,0x017b,0x017d,0x017d,0x017f,
    0x0243,0x0181,0x0182,0x0182,0x0184,0x0184,0x0186,0x0187,
    0x0187,0x0189,0x018a,0x018b,0x018b,0x018d,0x018e,0x018f,
    0x0190,0x0191,0x0191,0x0193,0x0194,0x01f6,0x0196,0x0197,
    0x0198,0x0198,0x023d,0x019b,0x019c,0x019d,0x0220,0x019f,
    0x01a0,0x01a0,0x01a2,0x01a2,0x01a4,0x01a4,0x01a6,0x01a7,
    0x01a7,0x01a9,0x01aa,0x01ab,0x01ac,0x01ac,0x01ae,0x01af,
    0x01af,0x01b1,0x01b2,0x01b3,0x01b3,0x01b5,0x01b5,0x01b7,
    0x01b8,0x01b8,0x01ba,0x01bb,0x01bc,0x01bc,0x01be,0x01f7,
    0x01c0,0x01c1,0x01c2,0x01c3,0x01c4,0x01c5,0x01c4,0x01c7,
    0x01c8,0x01c7,0x01ca,0x01cb,0x01ca,0x01cd,0x01cd,0x01cf,
    0x01cf,0x01d1,0x01d1,0x01d3,0x01d3,0x01d5,0x01d5,0x01d7,
    0x01d7,0x01d9,0x01d9,0x01db,0x01db,0x018e,0x01de,0x01de,
    0x01e0,0x01e0,0x01e2,0x01e2,0x01e4,0x01e4,0x01e6,0x01e6,
    0x01e8,0x01e8,0x01ea,0x01ea,0x01ec,0x01ec,0x01ee,0x01ee,
    0x01f0,0x01f1,0x01f2,0x01f1,0x01f4,0x01f4,0x01f6,0x01f7,
    0x01f8,0x01f8,0x01fa,0x01fa,0x01fc,0x01fc,0x01fe,0x01fe,
    0x0200,0x0200,0x0202,0x0202,0x0204,0x0204,0x0206,0x0206,
    0x0208,0x0208,0x020a,0x020a,0x020c,0x020c,0x020e,0x020e,
    0x0210,0x0210,0x0212,0x0212,0x0214,0x0214,0x0216,0x0216,
    0x0218,0x0218,0x021a,0x021a,0x021c,0x021c,0x021e,0x021e,
    0x0220,0x0221,0x0222,0x0222,0x0224,0x0224,0x0226,0x0226,
    0x0228,0x0228,0x022a,0x022a,0x022c,0x022c,0x022e,0x022e,
    0x0230,0x0230,0x0232,0x0232,0x0234,0x0235,0x0236,0x0237,
    0x0238,0x0239,0x2c65,0x023b,0x023b,0x023d,0x2c66,0x023f,
    0x0240,0x0241,0x0241,0x0243,0x0244,0x0245,0x0246,0x0246,
    0x0248,0x0248,0x024a,0x024a,0x024c,0x024c,0x024e,0x024e,
    0x0250,0x0251,0x0252,0x0181,0x0186,0x0255,0x0189,0x018a,
    0x0258,0x018f,0x025a,0x0190,0x025c,0x025d,0x025e,0x025f,
    0x0193,0x0261,0x0262,0x0194,0x0264,0x0265,0x0266,0x0267,
    0x0197,0x0196,0x026a,0x2c62,0x026c,0x026d,0x026e,0x019c,
    0x0270,0x0271,0x019d,0x0273,0x0274,0x019f,0x0276,0x0277,
    0x0278,0x0279,0x027a,0x027b,0x027c,0x2c64,0x027e,0x027f,
    0x01a6,0x0281,0x0282,0x01a9,0x0284,0x0285,0x0286,0x0287,
    0x01ae,0x0244,0x01b1,0x01b2,0x0245,0x028d,0x028e,0x028f,
    0x0290,0x0291,0x01b7,0x0293,0x0294,0x0295,0x0296,0x0297,
    0x0298,0x0299,0x029a,0x029b,0x029c,0x029d,0x029e,0x029f,
    0x02a0,0x02a1,0x02a2,0x02a3,0x02a4,0x02a5,0x02a6,0x02a7,
    0x02a8,0x02a9,0x02aa,0x02ab,0x02ac,0x02ad,0x02ae,0x02af,
    0x02b0,0x02b1,0x02b2,0x02b3,0x02b4,0x02b5,0x02b6,0x02b7,
    0x02b8,0x02b9,0x02ba,0x02bb,0x02bc,0x02bd,0x02be,0x02bf,
    0x02c0,0x02c1,0x02c2,0x02c3,0x02c4,0x02c5,0x02c6,0x02c7,
    0x02c8,0x02c9,0x02ca,0x02cb,0x02cc,0x02cd,0x02ce,0x02cf,
    0x02d0,0x02d1,0x02d2,0x02d3,0x02d4,0x02d5,0x02d6,0x02d7,
    0x02d8,0x02d9,0x02da,0x02db,0x02dc,0x02dd,0x02de,0x02df,
    0x02e0,0x02e1,0x02e2,0x02e3,0x02e4,0x02e5,0x02e6,0x02e7,
    0x02e8,0x02e9,0x02ea,0x02eb,0x02ec,0x02ed,0x02ee,0x02ef,
    0x02f0,0x02f1,0x02f2,0x02f3,0x02f4,0x02f5,0x02f6,0x02f7,
    0x02f8,0x02f9,0x02fa,0x02fb,0x02fc,0x02fd,0x02fe,0x02ff,
    0x0300,0x0301,0x0302,0x0303,0x0304,0x0305,0x0306,0x0307,
    0x0308,0x0309,0x030a,0x030b,0x030c,0x030d,0x030e,0x030f,
    0x0310,0x0311,0x0312,0x0313,0x0314,0x0315,0x0316,0x0317,
    0x0318,0x0319,0x031a,0x031b,0x031c,0x031d,0x031e,0x031f,
    0x0320,0x0321,0x0322,0x0323,0x0324,0x0325,0x0326,0x0327,
    0x0328,0x0329,0x032a,0x032b,0x032c,0x032d,0x032e,0x032f,
    0x0330,0x0331,0x0332,0x0333,0x0334,0x0335,0x0336,0x0337,
    0x0338,0x0339,0x033a,0x033b,0x033c,0x033d,0x033e,0x033f,
    0x0340,0x0341,0x0342,0x0343,0x0344,0x0345,0x0346,0x0347,
    0x0348,0x0349,0x034a,0x034b,0x034c,0x034d,0x034e,0x034f,
    0x0350,0x0351,0x0352,0x0353,0x0354,0x0355,0x0356,0x0357,
    0x0358,0x0359,0x035a,0x035b,0x035c,0x035d,0x035e,0x035f,
    0x0360,0x0361,0x0362,0x0363,0x0364,0x0365,0x0366,0x0367,
    0x0368,0x0369,0x036a,0x036b,0x036c,0x036d,0x036e,0x036f,
    0x0370,0x0371,0x0372,0x0373,0x0374,0x0375,0x0376,0x0377,
    0x0378,0x0379,0x037a,0x03fd,0x03fe,0x03ff,0x037e,0x037f,
    0x0380,0x0381,0x0382,0x0383,0x0384,0x0385,0x0386,0x0387,
    0x0388,0x0389,0x038a,0x038b,0x038c,0x038d,0x038e,0x038f,
    0x0390,0x0391,0x0392,0x0393,0x0394,0x0395,0x0396,0x0397,
    0x0398,0x0399,0x039a,0x039b,0x039c,0x039d,0x039e,0x039f,
    0x03a0,0x03a1,0x03a2,0x03a3,0x03a4,0x03a5,0x03a6,0x03a7,
    0x03a8,0x03a9,0x03aa,0x03ab,0x0386,0x0388,0x0389,0x038a,
    0x03b0,0x0391,0x0392,0x0393,0x0394,0x0395,0x0396,0x0397,
    0x0398,0x0399,0x039a,0x039b,0x039c,0x039d,0x039e,0x039f,
    0x03a0,0x03a1,0x03a3,0x03a3,0x03a4,0x03a5,0x03a6,0x03a7,
    0x03a8,0x03a9,0x03aa,0x03ab,0x038c,0x038e,0x038f,0x03cf,
    0x03d0,0x03d1,0x03d2,0x03d3,0x03d4,0x03d5,0x03d6,0x03d7,
    0x03d8,0x03d8,0x03da,0x03da,0x03dc,0x03dc,0x03de,0x03de,
    0x03e0,0x03e0,0x03e2,0x03e2,0x03e4,0x03e4,0x03e6,0x03e6,
    0x03e8,0x03e8,0x03ea,0x03ea,0x03ec,0x03ec,0x03ee,0x03ee,
    0x03f0,0x03f1,0x03f9,0x03f3,0x03f4,0x03f5,0x03f6,0x03f7,
    0x03f7,0x03f9,0x03fa,0x03fa,0x03fc,0x03fd,0x03fe,0x03ff,
    0x0400,0x0401,0x0402,0x0403,0x0404,0x0405,0x0406,0x0407,
    0x0408,0x0409,0x040a,0x040b,0x040c,0x040d,0x040e,0x040f,
    0x0410,0x0411,0x0412,0x0413,0x0414,0x0415,0x0416,0x0417,
    0x0418,0x0419,0x041a,0x041b,0x041c,0x041d,0x041e,0x041f,
    0x0420,0x0421,0x0422,0x0423,0x0424,0x0425,0x0426,0x0427,
    0x0428,0x0429,0x042a,0x042b,0x042c,0x042d,0x042e,0x042f,
    0x0410,0x0411,0x0412,0x0413,0x0414,0x0415,0x0416,0x0417,
    0x0418,0x0419,0x041a,0x041b,0x041c,0x041d,0x041e,0x041f,
    0x0420,0x0421,0x0422,0x0423,0x0424,0x0425,0x0426,0x0427,
    0x0428,0x0429,0x042a,0x042b,0x042c,0x042d,0x042e,0x042f,
    0x0400,0x0401,0x0402,0x0403,0x0404,0x0405,0x0406,0x0407,
    0x0408,0x0409,0x040a,0x040b,0x040c,0x040d,0x040e,0x040f,
    0x0460,0x0460,0x0462,0x0462,0x0464,0x0464,0x0466,0x0466,
    0x0468,0x0468,0x046a,0x046a,0x046c,0x046c,0x046e,0x046e,
    0x0470,0x0470,0x0472,0x0472,0x0474,0x0474,0x0476,0x0476,
    0x0478,0x0478,0x047a,0x047a,0x047c,0x047c,0x047e,0x047e,
    0x0480,0x0480,0x0482,0x0483,0x0484,0x0485,0x0486,0x0487,
    0x0488,0x0489,0x048a,0x048a,0x048c,0x048c,0x048e,0x048e,
    0x0490,0x0490,0x0492,0x0492,0x0494,0x0494,0x0496,0x0496,
    0x0498,0x0498,0x049a,0x049a,0x049c,0x049c,0x049e,0x049e,
    0x04a0,0x04a0,0x04a2,0x04a2,0x04a4,0x04a4,0x04a6,0x04a6,
    0x04a8,0x04a8,0x04aa,0x04aa,0x04ac,0x04ac,0x04ae,0x04ae,
    0x04b0,0x04b0,0x04b2,0x04b2,0x04b4,0x04b4,0x04b6,0x04b6,
    0x04b8,0x04b8,0x04ba,0x04ba,0x04bc,0x04bc,0x04be,0x04be,
    0x04c0,0x04c1,0x04c1,0x04c3,0x04c3,0x04c5,0x04c5,0x04c7,
    0x04c7,0x04c9,0x04c9,0x04cb,0x04cb,0x04cd,0x04cd,0x04c0,
    0x04d0,0x04d0,0x04d2,0x04d2,0x04d4,0x04d4,0x04d6,0x04d6,
    0x04d8,0x04d8,0x04da,0x04da,0x04dc,0x04dc,0x04de,0x04de,
    0x04e0,0x04e0,0x04e2,0x04e2,0x04e4,0x04e4,0x04e6,0x04e6,
    0x04e8,0x04e8,0x04ea,0x04ea,0x04ec,0x04ec,0x04ee,0x04ee,
    0x04f0,0x04f0,0x04f2,0x04f2,0x04f4,0x04f4,0x04f6,0x04f6,
    0x04f8,0x04f8,0x04fa,0x04fa,0x04fc,0x04fc,0x04fe,0x04fe,
    0x0500,0x0500,0x0502,0x0502,0x0504,0x0504,0x0506,0x0506,
    0x0508,0x0508,0x050a,0x050a,0x050c,0x050c,0x050e,0x050e,
    0x0510,0x0510,0x0512,0x0512,0x0514,0x0515,0x0516,0x0517,
    0x0518,0x0519,0x051a,0x051b,0x051c,0x051d,0x051e,0x051f,
    0x0520,0x0521,0x0522,0x0523,0x0524,0x0525,0x0526,0x0527,
    0x0528,0x0529,0x052a,0x052b,0x052c,0x052d,0x052e,0x052f,
    0x0530,0x0531,0x0532,0x0533,0x0534,0x0535,0x0536,0x0537,
    0x0538,0x0539,0x053a,0x053b,0x053c,0x053d,0x053e,0x053f,
    0x0540,0x0541,0x0542,0x0543,0x0544,0x0545,0x0546,0x0547,
    0x0548,0x0549,0x054a,0x054b,0x054c,0x054d,0x054e,0x054f,
    0x0550,0x0551,0x0552,0x0553,0x0554,0x0555,0x0556,0x0557,
    0x0558,0x0559,0x055a,0x055b,0x055c,0x055d,0x055e,0x055f,
    0x0560,0x0531,0x0532,0x0533,0x0534,0x0535,0x0536,0x0537,
    0x0538,0x0539,0x053a,0x053b,0x053c,0x053d,0x053e,0x053f,
    0x0540,0x0541,0x0542,0x0543,0x0544,0x0545,0x0546,0x0547,
    0x0548,0x0549,0x054a,0x054b,0x054c,0x054d,0x054e,0x054f,
    0x0550,0x0551,0x0552,0x0553,0x0554,0x0555,0x0556,0xffff,
    0x17f6,0x2c63,0x1d7e,0x1d7f,0x1d80,0x1d81,0x1d82,0x1d83,
    0x1d84,0x1d85,0x1d86,0x1d87,0x1d88,0x1d89,0x1d8a,0x1d8b,
    0x1d8c,0x1d8d,0x1d8e,0x1d8f,0x1d90,0x1d91,0x1d92,0x1d93,
    0x1d94,0x1d95,0x1d96,0x1d97,0x1d98,0x1d99,0x1d9a,0x1d9b,
    0x1d9c,0x1d9d,0x1d9e,0x1d9f,0x1da0,0x1da1,0x1da2,0x1da3,
    0x1da4,0x1da5,0x1da6,0x1da7,0x1da8,0x1da9,0x1daa,0x1dab,
    0x1dac,0x1dad,0x1dae,0x1daf,0x1db0,0x1db1,0x1db2,0x1db3,
    0x1db4,0x1db5,0x1db6,0x1db7,0x1db8,0x1db9,0x1dba,0x1dbb,
    0x1dbc,0x1dbd,0x1dbe,0x1dbf,0x1dc0,0x1dc1,0x1dc2,0x1dc3,
    0x1dc4,0x1dc5,0x1dc6,0x1dc7,0x1dc8,0x1dc9,0x1dca,0x1dcb,
    0x1dcc,0x1dcd,0x1dce,0x1dcf,0x1dd0,0x1dd1,0x1dd2,0x1dd3,
    0x1dd4,0x1dd5,0x1dd6,0x1dd7,0x1dd8,0x1dd9,0x1dda,0x1ddb,
    0x1ddc,0x1ddd,0x1dde,0x1ddf,0x1de0,0x1de1,0x1de2,0x1de3,
    0x1de4,0x1de5,0x1de6,0x1de7,0x1de8,0x1de9,0x1dea,0x1deb,
    0x1dec,0x1ded,0x1dee,0x1def,0x1df0,0x1df1,0x1df2,0x1df3,
    0x1df4,0x1df5,0x1df6,0x1df7,0x1df8,0x1df9,0x1dfa,0x1dfb,
    0x1dfc,0x1dfd,0x1dfe,0x1dff,0x1e00,0x1e00,0x1e02,0x1e02,
    0x1e04,0x1e04,0x1e06,0x1e06,0x1e08,0x1e08,0x1e0a,0x1e0a,
    0x1e0c,0x1e0c,0x1e0e,0x1e0e,0x1e10,0x1e10,0x1e12,0x1e12,
    0x1e14,0x1e14,0x1e16,0x1e16,0x1e18,0x1e18,0x1e1a,0x1e1a,
    0x1e1c,0x1e1c,0x1e1e,0x1e1e,0x1e20,0x1e20,0x1e22,0x1e22,
    0x1e24,0x1e24,0x1e26,0x1e26,0x1e28,0x1e28,0x1e2a,0x1e2a,
    0x1e2c,0x1e2c,0x1e2e,0x1e2e,0x1e30,0x1e30,0x1e32,0x1e32,
    0x1e34,0x1e34,0x1e36,0x1e36,0x1e38,0x1e38,0x1e3a,0x1e3a,
    0x1e3c,0x1e3c,0x1e3e,0x1e3e,0x1e40,0x1e40,0x1e42,0x1e42,
    0x1e44,0x1e44,0x1e46,0x1e46,0x1e48,0x1e48,0x1e4a,0x1e4a,
    0x1e4c,0x1e4c,0x1e4e,0x1e4e,0x1e50,0x1e50,0x1e52,0x1e52,
    0x1e54,0x1e54,0x1e56,0x1e56,0x1e58,0x1e58,0x1e5a,0x1e5a,
    0x1e5c,0x1e5c,0x1e5e,0x1e5e,0x1e60,0x1e60,0x1e62,0x1e62,
    0x1e64,0x1e64,0x1e66,0x1e66,0x1e68,0x1e68,0x1e6a,0x1e6a,
    0x1e6c,0x1e6c,0x1e6e,0x1e6e,0x1e70,0x1e70,0x1e72,0x1e72,
    0x1e74,0x1e74,0x1e76,0x1e76,0x1e78,0x1e78,0x1e7a,0x1e7a,
    0x1e7c,0x1e7c,0x1e7e,0x1e7e,0x1e80,0x1e80,0x1e82,0x1e82,
    0x1e84,0x1e84,0x1e86,0x1e86,0x1e88,0x1e88,0x1e8a,0x1e8a,
    0x1e8c,0x1e8c,0x1e8e,0x1e8e,0x1e90,0x1e90,0x1e92,0x1e92,
    0x1e94,0x1e94,0x1e96,0x1e97,0x1e98,0x1e99,0x1e9a,0x1e9b,
    0x1e9c,0x1e9d,0x1e9e,0x1e9f,0x1ea0,0x1ea0,0x1ea2,0x1ea2,
    0x1ea4,0x1ea4,0x1ea6,0x1ea6,0x1ea8,0x1ea8,0x1eaa,0x1eaa,
    0x1eac,0x1eac,0x1eae,0x1eae,0x1eb0,0x1eb0,0x1eb2,0x1eb2,
    0x1eb4,0x1eb4,0x1eb6,0x1eb6,0x1eb8,0x1eb8,0x1eba,0x1eba,
    0x1ebc,0x1ebc,0x1ebe,0x1ebe,0x1ec0,0x1ec0,0x1ec2,0x1ec2,
    0x1ec4,0x1ec4,0x1ec6,0x1ec6,0x1ec8,0x1ec8,0x1eca,0x1eca,
    0x1ecc,0x1ecc,0x1ece,0x1ece,0x1ed0,0x1ed0,0x1ed2,0x1ed2,
    0x1ed4,0x1ed4,0x1ed6,0x1ed6,0x1ed8,0x1ed8,0x1eda,0x1eda,
    0x1edc,0x1edc,0x1ede,0x1ede,0x1ee0,0x1ee0,0x1ee2,0x1ee2,
    0x1ee4,0x1ee4,0x1ee6,0x1ee6,0x1ee8,0x1ee8,0x1eea,0x1eea,
    0x1eec,0x1eec,0x1eee,0x1eee,0x1ef0,0x1ef0,0x1ef2,0x1ef2,
    0x1ef4,0x1ef4,0x1ef6,0x1ef6,0x1ef8,0x1ef8,0x1efa,0x1efb,
    0x1efc,0x1efd,0x1efe,0x1eff,0x1f08,0x1f09,0x1f0a,0x1f0b,
    0x1f0c,0x1f0d,0x1f0e,0x1f0f,0x1f08,0x1f09,0x1f0a,0x1f0b,
    0x1f0c,0x1f0d,0x1f0e,0x1f0f,0x1f18,0x1f19,0x1f1a,0x1f1b,
    0x1f1c,0x1f1d,0x1f16,0x1f17,0x1f18,0x1f19,0x1f1a,0x1f1b,
    0x1f1c,0x1f1d,0x1f1e,0x1f1f,0x1f28,0x1f29,0x1f2a,0x1f2b,
    0x1f2c,0x1f2d,0x1f2e,0x1f2f,0x1f28,0x1f29,0x1f2a,0x1f2b,
    0x1f2c,0x1f2d,0x1f2e,0x1f2f,0x1f38,0x1f39,0x1f3a,0x1f3b,
    0x1f3c,0x1f3d,0x1f3e,0x1f3f,0x1f38,0x1f39,0x1f3a,0x1f3b,
    0x1f3c,0x1f3d,0x1f3e,0x1f3f,0x1f48,0x1f49,0x1f4a,0x1f4b,
    0x1f4c,0x1f4d,0x1f46,0x1f47,0x1f48,0x1f49,0x1f4a,0x1f4b,
    0x1f4c,0x1f4d,0x1f4e,0x1f4f,0x1f50,0x1f59,0x1f52,0x1f5b,
    0x1f54,0x1f5d,0x1f56,0x1f5f,0x1f58,0x1f59,0x1f5a,0x1f5b,
    0x1f5c,0x1f5d,0x1f5e,0x1f5f,0x1f68,0x1f69,0x1f6a,0x1f6b,
    0x1f6c,0x1f6d,0x1f6e,0x1f6f,0x1f68,0x1f69,0x1f6a,0x1f6b,
    0x1f6c,0x1f6d,0x1f6e,0x1f6f,0x1fba,0x1fbb,0x1fc8,0x1fc9,
    0x1fca,0x1fcb,0x1fda,0x1fdb,0x1ff8,0x1ff9,0x1fea,0x1feb,
    0x1ffa,0x1ffb,0x1f7e,0x1f7f,0x1f88,0x1f89,0x1f8a,0x1f8b,
    0x1f8c,0x1f8d,0x1f8e,0x1f8f,0x1f88,0x1f89,0x1f8a,0x1f8b,
    0x1f8c,0x1f8d,0x1f8e,0x1f8f,0x1f98,0x1f99,0x1f9a,0x1f9b,
    0x1f9c,0x1f9d,0x1f9e,0x1f9f,0x1f98,0x1f99,0x1f9a,0x1f9b,
    0x1f9c,0x1f9d,0x1f9e,0x1f9f,0x1fa8,0x1fa9,0x1faa,0x1fab,
    0x1fac,0x1fad,0x1fae,0x1faf,0x1fa8,0x1fa9,0x1faa,0x1fab,
    0x1fac,0x1fad,0x1fae,0x1faf,0x1fb8,0x1fb9,0x1fb2,0x1fbc,
    0x1fb4,0x1fb5,0x1fb6,0x1fb7,0x1fb8,0x1fb9,0x1fba,0x1fbb,
    0x1fbc,0x1fbd,0x1fbe,0x1fbf,0x1fc0,0x1fc1,0x1fc2,0x1fc3,
    0x1fc4,0x1fc5,0x1fc6,0x1fc7,0x1fc8,0x1fc9,0x1fca,0x1fcb,
    0x1fc3,0x1fcd,0x1fce,0x1fcf,0x1fd8,0x1fd9,0x1fd2,0x1fd3,
    0x1fd4,0x1fd5,0x1fd6,0x1fd7,0x1fd8,0x1fd9,0x1fda,0x1fdb,
    0x1fdc,0x1fdd,0x1fde,0x1fdf,0x1fe8,0x1fe9,0x1fe2,0x1fe3,
    0x1fe4,0x1fec,0x1fe6,0x1fe7,0x1fe8,0x1fe9,0x1fea,0x1feb,
    0x1fec,0x1fed,0x1fee,0x1fef,0x1ff0,0x1ff1,0x1ff2,0x1ff3,
    0x1ff4,0x1ff5,0x1ff6,0x1ff7,0x1ff8,0x1ff9,0x1ffa,0x1ffb,
    0x1ff3,0x1ffd,0x1ffe,0x1fff,0x2000,0x2001,0x2002,0x2003,
    0x2004,0x2005,0x2006,0x2007,0x2008,0x2009,0x200a,0x200b,
    0x200c,0x200d,0x200e,0x200f,0x2010,0x2011,0x2012,0x2013,
    0x2014,0x2015,0x2016,0x2017,0x2018,0x2019,0x201a,0x201b,
    0x201c,0x201d,0x201e,0x201f,0x2020,0x2021,0x2022,0x2023,
    0x2024,0x2025,0x2026,0x2027,0x2028,0x2029,0x202a,0x202b,
    0x202c,0x202d,0x202e,0x202f,0x2030,0x2031,0x2032,0x2033,
    0x2034,0x2035,0x2036,0x2037,0x2038,0x2039,0x203a,0x203b,
    0x203c,0x203d,0x203e,0x203f,0x2040,0x2041,0x2042,0x2043,
    0x2044,0x2045,0x2046,0x2047,0x2048,0x2049,0x204a,0x204b,
    0x204c,0x204d,0x204e,0x204f,0x2050,0x2051,0x2052,0x2053,
    0x2054,0x2055,0x2056,0x2057,0x2058,0x2059,0x205a,0x205b,
    0x205c,0x205d,0x205e,0x205f,0x2060,0x2061,0x2062,0x2063,
    0x2064,0x2065,0x2066,0x2067,0x2068,0x2069,0x206a,0x206b,
    0x206c,0x206d,0x206e,0x206f,0x2070,0x2071,0x2072,0x2073,
    0x2074,0x2075,0x2076,0x2077,0x2078,0x2079,0x207a,0x207b,
    0x207c,0x207d,0x207e,0x207f,0x2080,0x2081,0x2082,0x2083,
    0x2084,0x2085,0x2086,0x2087,0x2088,0x2089,0x208a,0x208b,
    0x208c,0x208d,0x208e,0x208f,0x2090,0x2091,0x2092,0x2093,
    0x2094,0x2095,0x2096,0x2097,0x2098,0x2099,0x209a,0x209b,
    0x209c,0x209d,0x209e,0x209f,0x20a0,0x20a1,0x20a2,0x20a3,
    0x20a4,0x20a5,0x20a6,0x20a7,0x20a8,0x20a9,0x20aa,0x20ab,
    0x20ac,0x20ad,0x20ae,0x20af,0x20b0,0x20b1,0x20b2,0x20b3,
    0x20b4,0x20b5,0x20b6,0x20b7,0x20b8,0x20b9,0x20ba,0x20bb,
    0x20bc,0x20bd,0x20be,0x20bf,0x20c0,0x20c1,0x20c2,0x20c3,
    0x20c4,0x20c5,0x20c6,0x20c7,0x20c8,0x20c9,0x20ca,0x20cb,
    0x20cc,0x20cd,0x20ce,0x20cf,0x20d0,0x20d1,0x20d2,0x20d3,
    0x20d4,0x20d5,0x20d6,0x20d7,0x20d8,0x20d9,0x20da,0x20db,
    0x20dc,0x20dd,0x20de,0x20df,0x20e0,0x20e1,0x20e2,0x20e3,
    0x20e4,0x20e5,0x20e6,0x20e7,0x20e8,0x20e9,0x20ea,0x20eb,
    0x20ec,0x20ed,0x20ee,0x20ef,0x20f0,0x20f1,0x20f2,0x20f3,
    0x20f4,0x20f5,0x20f6,0x20f7,0x20f8,0x20f9,0x20fa,0x20fb,
    0x20fc,0x20fd,0x20fe,0x20ff,0x2100,0x2101,0x2102,0x2103,
    0x2104,0x2105,0x2106,0x2107,0x2108,0x2109,0x210a,0x210b,
    0x210c,0x210d,0x210e,0x210f,0x2110,0x2111,0x2112,0x2113,
    0x2114,0x2115,0x2116,0x2117,0x2118,0x2119,0x211a,0x211b,
    0x211c,0x211d,0x211e,0x211f,0x2120,0x2121,0x2122,0x2123,
    0x2124,0x2125,0x2126,0x2127,0x2128,0x2129,0x212a,0x212b,
    0x212c,0x212d,0x212e,0x212f,0x2130,0x2131,0x2132,0x2133,
    0x2134,0x2135,0x2136,0x2137,0x2138,0x2139,0x213a,0x213b,
    0x213c,0x213d,0x213e,0x213f,0x2140,0x2141,0x2142,0x2143,
    0x2144,0x2145,0x2146,0x2147,0x2148,0x2149,0x214a,0x214b,
    0x214c,0x214d,0x2132,0x214f,0x2150,0x2151,0x2152,0x2153,
    0x2154,0x2155,0x2156,0x2157,0x2158,0x2159,0x215a,0x215b,
    0x215c,0x215d,0x215e,0x215f,0x2160,0x2161,0x2162,0x2163,
    0x2164,0x2165,0x2166,0x2167,0x2168,0x2169,0x216a,0x216b,
    0x216c,0x216d,0x216e,0x216f,0x2160,0x2161,0x2162,0x2163,
    0x2164,0x2165,0x2166,0x2167,0x2168,0x2169,0x216a,0x216b,
    0x216c,0x216d,0x216e,0x216f,0x2180,0x2181,0x2182,0x2183,
    0x2183,0xffff,0x034b,0x24b6,0x24b7,0x24b8,0x24b9,0x24ba,
    0x24bb,0x24bc,0x24bd,0x24be,0x24bf,0x24c0,0x24c1,0x24c2,
    0x24c3,0x24c4,0x24c5,0x24c6,0x24c7,0x24c8,0x24c9,0x24ca,
    0x24cb,0x24cc,0x24cd,0x24ce,0x24cf,0xffff,0x0746,0x2c00,
    0x2c01,0x2c02,0x2c03,0x2c04,0x2c05,0x2c06,0x2c07,0x2c08,
    0x2c09,0x2c0a,0x2c0b,0x2c0c,0x2c0d,0x2c0e,0x2c0f,0x2c10,
    0x2c11,0x2c12,0x2c13,0x2c14,0x2c15,0x2c16,0x2c17,0x2c18,
    0x2c19,0x2c1a,0x2c1b,0x2c1c,0x2c1d,0x2c1e,0x2c1f,0x2c20,
    0x2c21,0x2c22,0x2c23,0x2c24,0x2c25,0x2c26,0x2c27,0x2c28,
    0x2c29,0x2c2a,0x2c2b,0x2c2c,0x2c2d,0x2c2e,0x2c5f,0x2c60,
    0x2c60,0x2c62,0x2c63,0x2c64,0x2c65,0x2c66,0x2c67,0x2c67,
    0x2c69,0x2c69,0x2c6b,0x2c6b,0x2c6d,0x2c6e,0x2c6f,0x2c70,
    0x2c71,0x2c72,0x2c73,0x2c74,0x2c75,0x2c75,0x2c77,0x2c78,
    0x2c79,0x2c7a,0x2c7b,0x2c7c,0x2c7d,0x2c7e,0x2c7f,0x2c80,
    0x2c80,0x2c82,0x2c82,0x2c84,0x2c84,0x2c86,0x2c86,0x2c88,
    0x2c88,0x2c8a,0x2c8a,0x2c8c,0x2c8c,0x2c8e,0x2c8e,0x2c90,
    0x2c90,0x2c92,0x2c92,0x2c94,0x2c94,0x2c96,0x2c96,0x2c98,
    0x2c98,0x2c9a,0x2c9a,0x2c9c,0x2c9c,0x2c9e,0x2c9e,0x2ca0,
    0x2ca0,0x2ca2,0x2ca2,0x2ca4,0x2ca4,0x2ca6,0x2ca6,0x2ca8,
    0x2ca8,0x2caa,0x2caa,0x2cac,0x2cac,0x2cae,0x2cae,0x2cb0,
    0x2cb0,0x2cb2,0x2cb2,0x2cb4,0x2cb4,0x2cb6,0x2cb6,0x2cb8,
    0x2cb8,0x2cba,0x2cba,0x2cbc,0x2cbc,0x2cbe,0x2cbe,0x2cc0,
    0x2cc0,0x2cc2,0x2cc2,0x2cc4,0x2cc4,0x2cc6,0x2cc6,0x2cc8,
    0x2cc8,0x2cca,0x2cca,0x2ccc,0x2ccc,0x2cce,0x2cce,0x2cd0,
    0x2cd0,0x2cd2,0x2cd2,0x2cd4,0x2cd4,0x2cd6,0x2cd6,0x2cd8,
    0x2cd8,0x2cda,0x2cda,0x2cdc,0x2cdc,0x2cde,0x2cde,0x2ce0,
    0x2ce0,0x2ce2,0x2ce2,0x2ce4,0x2ce5,0x2ce6,0x2ce7,0x2ce8,
    0x2ce9,0x2cea,0x2ceb,0x2cec,0x2ced,0x2cee,0x2cef,0x2cf0,
    0x2cf1,0x2cf2,0x2cf3,0x2cf4,0x2cf5,0x2cf6,0x2cf7,0x2cf8,
    0x2cf9,0x2cfa,0x2cfb,0x2cfc,0x2cfd,0x2cfe,0x2cff,0x10a0,
    0x10a1,0x10a2,0x10a3,0x10a4,0x10a5,0x10a6,0x10a7,0x10a8,
    0x10a9,0x10aa,0x10ab,0x10ac,0x10ad,0x10ae,0x10af,0x10b0,
    0x10b1,0x10b2,0x10b3,0x10b4,0x10b5,0x10b6,0x10b7,0x10b8,
    0x10b9,0x10ba,0x10bb,0x10bc,0x10bd,0x10be,0x10bf,0x10c0,
    0x10c1,0x10c2,0x10c3,0x10c4,0x10c5,0xffff,0xd21b,0xff21,
    0xff22,0xff23,0xff24,0xff25,0xff26,0xff27,0xff28,0xff29,
    0xff2a,0xff2b,0xff2c,0xff2d,0xff2e,0xff2f,0xff30,0xff31,
    0xff32,0xff33,0xff34,0xff35,0xff36,0xff37,0xff38,0xff39,
    0xff3a,0xff5b,0xff5c,0xff5d,0xff5e,0xff5f,0xff60,0xff61,
    0xff62,0xff63,0xff64,0xff65,0xff66,0xff67,0xff68,0xff69,
    0xff6a,0xff6b,0xff6c,0xff6d,0xff6e,0xff6f,0xff70,0xff71,
    0xff72,0xff73,0xff74,0xff75,0xff76,0xff77,0xff78,0xff79,
    0xff7a,0xff7b,0xff7c,0xff7d,0xff7e,0xff7f,0xff80,0xff81,
    0xff82,0xff83,0xff84,0xff85,0xff86,0xff87,0xff88,0xff89,
    0xff8a,0xff8b,0xff8c,0xff8d,0xff8e,0xff8f,0xff90,0xff91,
    0xff92,0xff93,0xff94,0xff95,0xff96,0xff97,0xff98,0xff99,
    0xff9a,0xff9b,0xff9c,0xff9d,0xff9e,0xff9f,0xffa0,0xffa1,
    0xffa2,0xffa3,0xffa4,0xffa5,0xffa6,0xffa7,0xffa8,0xffa9,
    0xffaa,0xffab,0xffac,0xffad,0xffae,0xffaf,0xffb0,0xffb1,
    0xffb2,0xffb3,0xffb4,0xffb5,0xffb6,0xffb7,0xffb8,0xffb9,
    0xffba,0xffbb,0xffbc,0xffbd,0xffbe,0xffbf,0xffc0,0xffc1,
    0xffc2,0xffc3,0xffc4,0xffc5,0xffc6,0xffc7,0xffc8,0xffc9,
    0xffca,0xffcb,0xffcc,0xffcd,0xffce,0xffcf,0xffd0,0xffd1,
    0xffd2,0xffd3,0xffd4,0xffd5,0xffd6,0xffd7,0xffd8,0xffd9,
    0xffda,0xffdb,0xffdc,0xffdd,0xffde,0xffdf,0xffe0,0xffe1,
    0xffe2,0xffe3,0xffe4,0xffe5,0xffe6,0xffe7,0xffe8,0xffe9,
    0xffea,0xffeb,0xffec,0xffed,0xffee,0xffef,0xfff0,0xfff1,
    0xfff2,0xfff3,0xfff4,0xfff5,0xfff6,0xfff7,0xfff8,0xfff9,
    0xfffa,0xfffb,0xfffc,0xfffd,0xfffe,0xffff
);
my @upcase_table = upcase_table_expand(@upcase_table_msft);
my $upcase_table_msft = pack('v*', @upcase_table_msft);
my $upcase_table_minimal = pack('v*', upcase_table_compress(@upcase_table));
# Upcase an UTF-16LE string accoding to the expanded upcase table
sub upcase_longname($) {
    my($s) = @_;
    return pack('v*', map { $upcase_table[$_] } unpack('v*', $s));
}

# --- Utility functions ---

sub min(@) {
    my $x;
    foreach my $y (@_) {
	next unless (defined($y));
	$x = $y if (!defined($x) || $y < $x);
    }
    return $x;
}
sub max(@) {
    my $x;
    foreach my $y (@_) {
	next unless (defined($y));
	$x = $y if (!defined($x) || $y > $x);
    }
    return $x;
}

sub align_down($$) {
    my($val, $align) = @_;
    return $val - ($val % $align);
}
sub align_up($$) {
    my($val, $align) = @_;
    return align_down($val + $align - 1, $align);
}
sub div_up($$) {
    my($val, $divisor) = @_;
    return ($val + $divisor - 1)/$divisor;
}

# Code to kill a BIOS boot: 1: int $0x80; hlt; jmp 1b
my $KILLBOOT = "\xcd\x18\xf4\xeb\xfb";
my $KILLOFFS = 0x78;		# Per the exFAT specification 3.1.1
my $KILLJMP  = $KILLOFFS < 0x82
    ? pack("CCC", 0xeb, $KILLOFFS-2, 0x90)
    : pack("Cv",  0xe9, $KILLOFFS-3);

# Generate random or pseudorandom bytes depending on if --random is enabled
my $pseudo_random_buf;		# Hash or dummy seed; undef for random
sub rand_bytes($) {
    my($count) = @_;
    my $o;

    if (defined($pseudo_random_buf)) {
	$o = substr($pseudo_random_buf, 0, $count);
	$pseudo_random_buf = substr($pseudo_random_buf, $count);
    } elsif (Crypt::Random::Source->VERSION < 0.13) {
	$o = Crypt::Random::Source::get_strong($count);
    } else {
	$o = Crypt::Random::Source::get_weak($count);
    }
    die if (length($o) != $count);
    return $o;
}
# Yeah, the byte ordering here is weird as heck
sub guid(@) {
    my($a,$b,$c,$d,$e) = @_;
    return pack('VvvQ>', $a, $b, $c, ($d << 48) | $e);
}
sub rand_guid() {
    my @b = unpack('C*', rand_bytes(16));
    # Version 4: random, version 8: custom algorithm
    $b[7] = ($b[7] & 0x0f) | ($opt{random} ? 0x40 : 0x80);
    $b[8] = ($b[8] & 0x3f) | 0x80;
    return pack('C*', @b);
}
sub parse_guid($) {
    my($s) = @_;
    my @bpos = (3,2,1,0,5,4,7,6,8 ... 15);
    if ($s =~ s/^(0[xX]|\$)//i) {
	@bpos = (0 ... 15);
    }
    $s =~ s/[^a-z0-9]//ig;

    return rand_guid() if ($s eq '');

    my $guid = pack('.', 16);
    for (my $i = 0; $i < 16; $i++) {
	my $h = substr($s,$i << 1,2);
	substr($guid,$bpos[$i],1) = pack('C', hex $h);
    }
    return $guid;
}
sub guid_str($;$) {
    my($guid, $short) = @_;
    if ($short) {
	my @w = unpack('vv', $guid);
	return sprintf('%04X-%04X', $w[1], $w[0]);
    } else {
	my @w = unpack('VvvnnN', $guid."\0\0");
	return sprintf('%08X-%04X-%04X-%04X-%04X%08X', @w);
    }
}

sub to_chs($$) {
    my($lba, $chs) = @_;
    my $s = ($lba % $chs->[2]) + 1;
    my $t = $lba / $chs->[2];
    my $h = $t % $chs->[1];
    my $c = $t / $chs->[1];
    ($c,$h,$s) = (1023,$chs->[1]-1,$chs->[2]) if ($c > 1023);
    return pack('C3', $h, ($c >> 2)|$s, $c & 0xff);
}
# Create a minimal MBR partition table with one single partition.
sub gen_mbr($$$;$$$)
{
    my($part_start, $part_len, $part_type, $chs, $active, $id) = @_;
    my $part_end = min($part_start+$part_len-1, 0xffffffff);
    my $pe;
    if ($part_start <= 0xffffffff) {
	$pe = pack('Ca3Ca3VV',
		   $active ? 0x80 : 0, to_chs($part_start, $chs),
		   $part_type, to_chs($part_end, $chs), $part_start, $part_len);
    }
    return pack('a3.a*@440a4va64v', $KILLJMP, $KILLOFFS, $KILLBOOT,
		$id, 0, $pe, 0xaa55);
}

# Create a minimal GPT partition table with one single partition.
# Return a vector containing the PMBR, the GPT header,
# the GPT array, and the backup GPT header in that order.
# $gpt_len is the desired size of the GPT array in sectors.
sub gen_gpt($$$$$;$$$$) {
    my($disk_len, $part_start, $part_len, $part_type,
       $gpt_len, $part_name, $disk_guid, $part_guid, $chs) = @_;
    my @blks;

    # PMBR - per spec the PMBR has no disk ID
    $blks[0] = gen_mbr(1, $disk_len-1, 0xee, $chs);

    # GPT partition array
    $blks[2] = pack('a16a16Q<Q<Q<a142v.', $part_type, $part_guid,
		    $part_start, $part_start + $part_len - 1,
		    ($opt{efi} ? 1 : 0) | ($opt{active} ? 4 : 0),
		    to_uni($part_name), 0,
		    $gpt_len << 9);
    my $array_crc = crc32($blks[2]);

    # GPT headers (primary and alternate)
    my $disk_guid = rand_guid();
    my $backup_gpt = $disk_len - ($opt{backup_gpt} ? 1+$gpt_len : 0);
    my @hdrs = ([1, 1, $backup_gpt+$gpt_len, 2]);
    push(@hdrs, [3, $backup_gpt+$gpt_len, 1, $backup_gpt]) if ($opt{backup_gpt});
    foreach my $hdr (@hdrs) {
	my($i, $here, $there, $array) = @$hdr;
	$blks[$i] =
	    pack('a8VVVVQ<Q<Q<Q<a16Q<VVV', 'EFI PART', 0x10000, 92, 0, 0,
		 $here, $there, $gpt_len+2, $backup_gpt,
		 $disk_guid, $array, $gpt_len << 2, 128, $array_crc);

	my $hdr_crc = crc32($blks[$i]);
	substr($blks[$i], 16, 4) = pack('V', $hdr_crc);
	$blks[$i] .= ("\0" x (512 - length($blks[$i])));
    }

    return @blks;
}

# Create a normalization of a pathname as a list suitable for
# inserting in the directory tree.
sub pathlist($;$) {
    my($path, $from_host) = @_;
    my @rawpath;
    my($curdir, $updir);

    if ($from_host) {
	my @sp   = File::Spec->splitpath($path);
	my $cp   = File::Spec->catpath(undef, $sp[1], $sp[2]);
	@rawpath = File::Spec->splitdir($cp);
	$curdir  = File::Spec->curdir();
	$updir   = File::Spec->updir();
    } else {
	@rawpath = split(/[\/\\]+/, $path);
	$curdir  = '.';
	$updir   = '..';
    }

    my @path;
    foreach my $rp (@rawpath) {
	my $vn = $rp;
	if ($rp eq '' || $rp eq $curdir) {
	    next;
	} elsif ($rp eq $updir) {
	    pop(@path);
	} else {
	    push(@path, $rp);
	}
    }

    die "$myname: empty path?: \"$path\"" if (!@path);
    return @path;
}

# Error counter
my $err = 0;

# Directory entry types, and lists of nodes of each type
# Important: 0 < $EXFAT < $VOL < $DIR < $FILE
my $EXFAT   = 0x04;		# exFAT special entry
my $VOL     = 0x08;		# Volume label
my $DIR     = 0x10;
my $FILE    = 0x20;		# Abusing the ARCHIVE bit

# exFAT directory entry types
my $EXFAT_BITMAP = 0x81;
my $EXFAT_UPCASE = 0x82;
my $EXFAT_VOL    = 0x83;
my $EXFAT_FILE   = 0x85;
my $EXFAT_STREAM = 0xc0;
my $EXFAT_NAME   = 0xc1;
my $EXFAT_VOLID  = 0xa0;

# Normalize a filename coming from host space stripping things
# unsuitable for the FAT longname and forcing Perl to convert
# a string to characters rather than bytes
sub normalize_filename($) {
    my($vn) = @_;
    # Normalize name; this affects the retained name
    $vn = decode_utf8($vn);	# Convert to characters
    my $nn = $vn;
    $nn =~ s/[\x00-\x1f\x7f"*\/:<>?\\|]/_/g;
    $nn =~ s/^(\s)/_$1/;
    $nn =~ s/([\.\s])$/$1_/;
    if ($opt{strict}->{filename} && $nn ne $vn) {
	print STDERR "$opt{outfile}: invalid FAT filename: \"$vn\"\n";
	$err++;
    }
    return $nn;
}

sub set_node_type($$;$) {
    my($node, $type, $inpath) = @_;
    if ($node->{type}) {
	if ($node->{type} != $type) {
	    printf STDERR "%s: type conflict: %s (%s and %s)\n",
		$opt{outfile}, $node->{type} eq $DIR ? ' and directory' : '',
		$node->{path}, $node->{inpath}, $inpath;
	    return 0;
	}
    } else {
	$node->{type} = $type;
	push(@{$node->{root}->{nodes}->{$type}}, $node);
	$node->{files} = {} if ($type == $DIR);
    }
    $node->{inpath} = $inpath;
    return 1;
}

# Creates or finds a node in the directory tree from a list of names
# and returns a reference to it
sub mktreenode($@) {
    my($here, @path) = @_;

    foreach my $p (@path) {
	my $herepath = $here->{path};

	if (!set_node_type($here, $DIR)) {
	    print STDERR "$opt{outfile}: type conflict: $herepath\n";
	    $err++;
	    return undef;
	}

	my $f = normalize_filename($p);
	my $longname = genlongname($f);
	my $uf = upcase_longname($longname); # If this is the same, collision
	my $there = $here;
	$here = $there->{files}->{$uf};
	if (!defined($here)) {
	    # New directory entry, unknown type so far
	    $here = {
		'root' => $there->{root}, 'up' => $there, 'name' => $f,
		'longname' => $longname, 'path' => $herepath.'/'.$f
	    };
	    $there->{files}->{$uf} = $here;
	}
    }

    return $here;
}

sub inputnode($$) {
    my($root, $inpath) = @_;

    $inpath =~ s/^(?:([^:]*):)?://;
    my $destpath = $1;

    my @st = stat($inpath);
    if (!@st) {
	print STDERR "$inpath: path not found\n";
	$err++;
	return;
    }

    my @dest;
    if (!defined($destpath)) {
	@dest = ();
    } elsif ($destpath eq '') {
	@dest = pathlist($inpath, 1);
    } else {
	@dest = pathlist($destpath, 0);
    }

    my $dest = mktreenode($root, @dest);
    $dest->{inpath} = $inpath if (defined($dest));
    return $dest;
}

# Create the root directory node
sub rootdir() {
    my $root = {'path' => undef, 'name' => undef};
    $root->{root} = $root;
    $root->{nodes} = { $FILE => [], $VOL => [], $EXFAT => [], $DIR => [] };
    set_node_type($root, $DIR);
    return $root;
}

# Append a name to a directory path in host space
sub cat_path($$) {
    my($dir, $file) = @_;

    my @sp = File::Spec->splitpath($dir);
    $sp[1] = ($sp[1] eq '' || $sp[1] eq File::Spec->curdir)
	? $sp[2] : File::Spec->catdir($sp[1], $sp[2]);
    $sp[2] = $file;
    return File::Spec->catpath(@sp);
}

sub node_date($) {
    my($node) = @_;

    do {
	return $node->{date} if (defined($node->{date}));
    } while ($node = $node->{up});
    return 0;
}

# Add an input file or directory to a directory node
sub add_input($;$) {
    my($dest, $inpath) = @_;
    return undef unless (defined($dest));

    $inpath = $dest->{inpath} unless (defined($inpath));

    my @st = stat($inpath);
    if (!@st) {
	print STDERR "$opt{outfile}: input not found: $inpath\n";
	return;
    }
    my $mode = $st[2];

    if (S_ISDIR($mode)) {
	if (!set_node_type($dest, $DIR, $inpath)) {
	    $err++;
	    return;
	}
	my $dh;
	if (!opendir($dh, $inpath)) {
	    printf STDERR "%s: cannot scan input directory: %s: %s\n",
		$opt{outfile}, $inpath, $!;
	    $err++;
	    return;
	}
	while (defined(my $de = readdir($dh))) {
	    next if ($de eq File::Spec->curdir() || $de eq File::Spec->updir());
	    my $node = mktreenode($dest, $de);
	    add_input($node, cat_path($inpath, $de));
	}
	closedir($dh);
    } elsif (S_ISREG($mode)) {
	if (!set_node_type($dest, $FILE, $inpath)) {
	    $err++;
	    return;
	}
	$dest->{size} = $st[7];
	if ($st[7] > 0xffffffff) {
	    $dest->{size} = 0xffffffff;
	    $dest->{exsize} = $st[7];
	    $dest->{root}->{largefile} = 1;
	    if (!$opt{exfat}) {
		print STDERR "$opt{outfile}: file too large, need exFAT: $inpath\n";
		$err++;
	    }
	}
	# Readonly bit
	$dest->{flags} = !($mode & S_IWRITE) || $opt{ro} ? 1 : 0;
    } else {
	printf STDERR "$opt{outfile}: warning: skipping non-regular file: $inpath\n";
	return;
    }
    # Successful!
    $dest->{date} = max(node_date($dest), $st[9]);
}

#
# Read all the input files
#
sub read_inputs(@) {
    my(@inputs) = @_;

    my $root = rootdir();
    foreach my $input (@inputs) {
	add_input(inputnode($root, $input));
    }

    return $root;
}

# Convert a longname to UTF-16LE and pad it
sub genlongname($) {
    my($s) = @_;
    return substr(to_uni($s), 0, 255 << 1);
}

# Generate a short filename. For simplicity, only consider
# non-whitespace ASCII characters as valid shortname characters, even
# though that is needlessly strict. This also avoids the special handling
# of 0xe5 as the first byte. $shorthash is a reference to a
# hash for the directory within which the conversion is happening.

# For each string reference in the list, strip invalid shortname
# characters (including .) in place and return 1 if the conversion was
# inexact except for case.
sub to_valid_shortchar(@) {
    my $inexact = 0;
    foreach my $i (@_) {
	$$i = uc($$i);
	$inexact = 1 if ($$i =~ s/[^!\#-\)\-0-9\@-Z^-\{\}~]+/_/g);
    }
    return $inexact;
}

sub genshortname($$$) {
    my($fullname, $shorthash, $type) = @_;
    if ($type & ($VOL|$EXFAT)) {
	# The rules for volume labels are much less strict,
	# and exfat entries are irrelevant here
	(my $lbl = $fullname) =~ s/[^ -~]/_/g;
	return pack('A11', $lbl);
    }

    # If this doesn't match then the name is empty...
    die unless ($fullname =~ /^(.+?)(?:\.([^.]+))?$/);
    my $head = $1;
    my $ext  = $2;
    my $inexact = to_valid_shortchar(\$head, \$ext);

    $inexact = 1 if ($head eq '' || length($head) > 8);
    $inexact = 1 if (length($ext) > 3);

    for (my $ctr = $inexact; $ctr <= 99999; $ctr++) {
	my $ctrtail = $ctr ? '~'.$ctr : '';
	my $xhead = substr($head, 0, 8-length($ctrtail));
	my $xname = pack('A8A3', $xhead.$ctrtail, $ext);
	if (!defined($shorthash->{$xname})) {
	    if ($opt{verbose} >= 2) {
		print STDERR "$opt{outfile}: short \"$xname\" for \"$fullname\"\n";
	    }
	    $shorthash->{$xname} = $fullname;
	    return $xname;
	}
    }

    die "$opt{outfile}: directory too long\n";
}

# Compute directory sizes and generate the sorted list of
# directory entries, and assign short names (long names should already
# have been assigned)
sub prep_directories($) {
    my($root) = @_;

    # Add exFAT special directory entries (ignored for FAT).
    # Data is filled in later on...
    $root->{fatchain} = 1;
    my $exfat_bitmap = {'root' => $root, 'up' => $root, 'fatchain' => 1,
			'extype' => $EXFAT_BITMAP, 'name' => ':bitmap',
			'path' => '<bitmap>'};
    set_node_type($root->{files}->{"\0\0bitmap"} = $exfat_bitmap, $EXFAT);
    my $exfat_upcase = {'root' => $root, 'up' => $root, 'fatchain' => 1,
			'extype' => $EXFAT_UPCASE, 'name' => ':upcase',
			'path' => '<upcase>'};
    set_node_type($root->{files}->{"\0\0upcase"} = $exfat_upcase, $EXFAT);
    my $exfat_volid = {'root' => $root, 'up' => $root, 'name' => ':volid',
		       'extype' => $EXFAT_VOLID, 'path' => '<volid>'};
    set_node_type($root->{files}->{"\0\0volid"} = $exfat_volid, $EXFAT);

    foreach my $d (@{$root->{nodes}->{$DIR}}) {
	my %shorthash;
	my @filelist = sort {
	    $a->{type} <=> $b->{type} || $a->{name} cmp $b->{name}
        } values(%{$d->{files}});
	$d->{filelist} = \@filelist;

	my $e  = 2;		# . and ..
	my $ee = 0;
	# FAT:   One entry for each 13 UTF-16 codepoints, plus one
	# exFAT: One entry for each 15 UTF-16 codepoints, plus two
	foreach my $f (@filelist) {
	    $f->{shortname} = genshortname($f->{name}, \%shorthash, $f->{type});
	    $e +=  $f->{type} != $EXFAT
		? div_up(length($f->{longname}), 26) + 1 : 0;
	    $ee += $f->{type} > $VOL
		? div_up(length($f->{longname}), 30) + 2 : 1;
	}
	$d->{size}   = $e << 5;
	$d->{exsize} = $ee << 5;
    }

    $root->{size}   -= 2 << 5;	# No . and .. in the root
}

sub count_clusters($$;$) {
    my($root, $cbytes, $exfat) = @_;
    my $clust = 0;
    # Iterate over all nodes of all types
    foreach my $f (map { @$_ } values(%{$root->{nodes}})) {
	my $size;
	die if ($f->{type} == $EXFAT && $f->{size});
	if ($exfat) {
	    $size = defined($f->{exsize}) ? $f->{exsize} : $f->{size};
	} else {
	    $size = $f->{size};
	}
	$clust += div_up($size, $cbytes);
    }
    return $clust;
}

# GPT partition types
my @gpt_types = (
    # Microsoft basic data type (including exFAT)
    guid(0xebd0a0a2, 0xb9e5, 0x4433, 0x87c0, 0x68b6b72699c7),
    # EFI system type
    guid(0xc12a7328, 0xf81f, 0x11d2, 0xba4b, 0x00a0c93ec93b),
);

# Create preliminary format parameters for a specific cluster size.
# These will be made exact by finalize_format_params() later.
sub get_format_params($$;$) {
    my($root, $cshift, $exfat) = @_;

    return undef unless ($cshift <= 7 || $exfat);

    my $secshift = 9;
    my $sector = 1 << $secshift;
    my $csize = 1 << $cshift;
    my $cbshift = $cshift + $secshift;
    my $cbytes = 1 << $cbshift;
    my $salign = $opt{align} ? $csize : 1;
    my $balign = $salign << $secshift;

    my $c = count_clusters($root, $cbytes, $exfat);
    my $fattype;
    my $nfats = (!$exfat && $opt{strict}->{fats}) ? 2 : 1;

    # Partition table alignment quantum
    my $partalign = max($salign, $opt{strict}->{partspace} ? 64 : 1);

    # Reserved space; if the "extra files" part is set make sure there is at
    # least one free cluster per file, and enough space to put any
    # reserved files in subdirectories.
    #
    # Syntax: --reserve=<files>,<bytes>,<rootfiles>,<namelen>
    my @res = $opt{reserve} ? @{$opt{reserve}} : ();
    $res[0] = max($res[0], $res[2]);
    my $dirbytesperfile;
    if ($exfat) {
	$dirbytesperfile = (2 + div_up($res[3] || 64, 15)) << 5;
    } else {
	$dirbytesperfile = (1 + div_up($res[3] || 64, 13)) << 5;
    }
    $c += $res[0] + div_up($res[1], $cbytes);
    $c += div_up($dirbytesperfile * $res[0], $cbytes);

    my $rootclusts = div_up($root->{size}, $cbytes);
    $c -= $rootclusts unless ($exfat || $c - $rootclusts >= 0xfff5);

    # FAT type == bits per entry
    my $strictminsize;
    if ($exfat) {
	$fattype = 32;
	$strictminsize = 1 << 20;
    } elsif ($c < 0xff5) {
	$fattype = 12;
    } elsif ($c < 0xfff5) {
	$fattype = 16;
    } elsif ($c < 0x0ffffff5) {
	$fattype = 32;
	$strictminsize = 1 << 29;
	return undef if ($cshift < 3 && $opt{strict}->{cluster});
    } else {
	return undef;		# Does not fit
    }

    # exFAT fixed structures
    my $bitmapclust;
    my $upcase_table;
    if ($exfat) {
	# Upcase table
	$upcase_table =
	    $opt{strict}->{upcase} || $cbytes >= length($upcase_table_msft)
	    ? \$upcase_table_msft : \$upcase_table_minimal;
	$c += div_up(length($$upcase_table), $cbytes);

	# Allocation bitmap. This one is "fun", as its size depends on
	# the cluster count but might itself increase the necessary cluster
	# count...
	my $needbitmap;
	do {
	    $bitmapclust = $needbitmap;
	    $needbitmap = div_up($c + $bitmapclust, $cbytes << 3);
	} while ($needbitmap > $bitmapclust);
	$c += $bitmapclust;
    }

    # For FAT32 and exFAT, enforce a minimum size if strict
    $c = max($c, div_up($strictminsize, $cbytes)) if ($opt{strict});
    my $sectors = $c << $cshift;

    # Size of each FAT
    my $fatsize = div_up(($c+2)*$fattype, 8);		 # Bytes
    $fatsize = align_up($fatsize, $balign) >> $secshift; # Sectors
    $sectors += $nfats*$fatsize;

    # Reserved sectors (in the FAT sense)
    my $resv = 1;
    $resv = $opt{strict}->{bootspace} ? 32 : 8 if ($fattype == 32);
    $resv = 24 if ($exfat);
    $resv = align_up($resv, $salign);
    $sectors += $resv;

    # The root directory for FAT12/16
    my $rootdirsize;
    if ($fattype < 32) {
	$rootdirsize = $root->{size} + $res[2] * $dirbytesperfile;
	if ($opt{strict}->{rootdir}) {
	    $rootdirsize = max($rootdirsize, ($fattype < 16 ? 256 : 512) << 5);
	}
	$rootdirsize = align_up($rootdirsize, $balign);
	$sectors += $rootdirsize >> $secshift;
    }

    # Crude attempt at geometry, in case someone actually that still cares...
    my $chs = [];
    my $chsec = $sectors + ($opt{part} == $FLAT ? 0 : $partalign);
    my $floppy = $opt{part} == $FLAT && $chsec <= 10080 && !$exfat;
    $chs->[1] = $opt{h} || ($floppy ? ($chsec < 512 ? 1 : 2) : 255);
    $chs->[2] = $opt{s} ||
	($floppy ? div_up($chsec, $chs->[1]*($chsec < 1024 ? 40 : 80)) : 63);
    $chs->[0] = div_up($chsec, $chs->[1]*$chs->[2]);
    $floppy = 0 if ($chs->[0] > 255);

    # Suitable MBR type
    my $mbrtype;
    if ($opt{efi}) {
	$mbrtype = 0xef;	# For MBR only, not PMBR
    } elsif ($exfat) {
	$mbrtype = 0x07;
    } elsif ($fattype == 12) {
	$mbrtype = 0x01;
    } elsif ($fattype == 16) {
	$mbrtype =
	    $sectors < 0x10000 ? 0x04 : $chs->[0] <= 1024 ? 0x06 : 0x0e;
    } else {
	$mbrtype = 0x0c;
    }

    my $media = $floppy ? 0xf0 : 0xf8;
    my $drive = $floppy ? 0x00 : 0x80;

    # Generated filesystem image parameters. May be an overestimate.
    return {
	'root' => $root, 'rootdirsize' => $rootdirsize,
	'bitmapclust' => $bitmapclust, 'exfat' => $exfat,
	'upcase' => $upcase_table,
	'sectors' => $sectors, 'cshift' => $cshift,
	'csectors' => 1 << $cshift, 'cbytes' => $cbytes,
	'cbshift' => $cbshift, 'fattype' => $fattype,
	'resv' => $resv, 'nfats' => $nfats, 'fatsize' => $fatsize,
	'clust' => $c, 'secshift' => $secshift, 'sector' => $sector,
	'mbrtype' => $mbrtype, 'gpttype' => $gpt_types[!!$opt{efi}],
	'partalign' => $partalign,
	'chs' => $chs, 'media' => $media, 'drive' => $drive
    };
}

# Update the format parameters after the disk layout is complete
sub finalize_format_params($$$) {
    my($root, $startsec, $fssec) = @_;
    my $parm = $root->{parm};
    $parm->{hidden}  = $startsec;

    my $prefixsec = $parm->{resv} + $parm->{fatsize} * $parm->{nfats};
    $prefixsec += div_up($parm->{rootdirsize}, $parm->{sector});

    if ($parm->{exfat}) {
	$root->{files}->{"\0\0bitmap"}->{exsize} = div_up($parm->{clust}, 8);
	$root->{files}->{"\0\0upcase"}->{data} = ${$parm->{upcase}};
    }

    # Offset to the data area (cluster heap) in partition-relative
    # sectors and in disk-absolute bytes (the latter to print messages)
    $parm->{heap} = $prefixsec;
    $parm->{dataoffset} = ($startsec + $prefixsec) << $parm->{secshift};

    # Compute the final size of the filesystem
    my $fattypemax = (1 << $parm->{fattype}) - 12;
    my $fatsizemax = ($parm->{fatsize} << ($parm->{secshift} + 3))
	/ $parm->{fattype};
    my $spacemax = ($fssec - $prefixsec) >> $parm->{cshift};
    my $clust = min($spacemax, $fattypemax, $fatsizemax);

    if ($parm->{exfat}) {
	my $bitmaskmax = $parm->{bitmapclust} << ($parm->{cbshift} + 3);
	$clust = min($clust, $bitmaskmax);
    }
    $parm->{clust}   = $clust;
    $parm->{sectors} = $prefixsec + ($clust << $parm->{cshift});
}

# This walks the *subnodes* of a node of a certain type
sub populate_with($$$$) {
    my($dir, $parm, $type, $what) = @_;
    my $ret = 0;

    foreach my $de (@{$dir->{filelist}}) {
	if ($de->{type} == $type) {
	    $ret += $what->($de, $parm);
	}
	if ($de->{type} == $DIR) {
	    $ret += populate_with($de, $parm, $type, $what);
	}
    }
    return $ret;
}

# Assign cluster number to one file node
sub assign_clust($) {
    my($f) = @_;
    my $root = $f->{root};
    my $parm = $root->{parm};
    my $size = $f->{size};

    $size = $f->{exsize} if ($parm->{exfat} && defined($f->{exsize}));
    $size = length($f->{data}) if (!defined($size) && defined($f->{data}));
    $f->{size} = $size;

    $f->{fatchain} = 1 unless ($parm->{exfat});

    $f->{paddedsize} = align_up($f->{size}, $parm->{cbytes});
    $f->{nclust} = $f->{paddedsize} >> $parm->{cbshift};
    if (!$f->{nclust}) {
	$f->{clustno} = 0;
    } else {
	my $clustno = $f->{clustno} = $parm->{endclust};
	$parm->{endclust} += $f->{nclust};
	push(@{$root->{layout}}, $f);
	if ($opt{verbose} >= 3) {
	    printf STDERR "%s: cluster %5d @ 0x%08x (len %5d) for \"%s\"\n",
		$opt{outfile}, $clustno,
		$parm->{dataoffset} + (($clustno-2) << $parm->{cbshift}),
		$f->{nclust}, $f->{path} || '/';
	}
    }
    return $f->{nclust};
}

# Assign final cluster numbers and padded sizes to files and directories
sub assign_clusters($) {
    my($root) = @_;
    my $parm = $root->{parm};

    $parm->{endclust} = 2;	# First real cluster
    $root->{layout} = [];	# File nodes in layout order

    populate_with($root, $parm, $EXFAT, \&assign_clust);
    if ($parm->{rootdirsize}) {
	# FAT12/16: root directory is special
	$root->{clustno} = 0;
	$root->{paddedsize} = $parm->{rootdirsize};
	push(@{$root->{layout}}, $root);
	if ($opt{verbose} >= 3) {
	    printf STDERR "%s: root directory @ 0x%08x, %d sectors\n",
		$opt{outfile}, $parm->{dataoffset} - $root->{paddedsize},
		$root->{paddedsize} >> $parm->{secshift};
	}
    } else {
	assign_clust($root);
    }
    populate_with($root, $parm, $DIR,   \&assign_clust);
    populate_with($root, $parm, $FILE,  \&assign_clust);

    if ($opt{verbose} >= 1) {
	printf STDERR "%s: %d/%d clusters free\n",
	    $opt{outfile}, $parm->{clust} - ($parm->{endclust}-2),
	    $parm->{clust};
    }
    die if ($parm->{endclust} > $parm->{clust} + 2);

    if ($parm->{exfat}) {
	my $usedclust = $parm->{endclust} - 2;
	my $bitmap = "\xff" x ($usedclust >> 3);
	if ($usedclust & 7) {
	    $bitmap .= pack('C', (1 << ($usedclust & 7))-1);
	}
	$bitmap .= "\0" x (div_up($parm->{clust}, 8) - length($bitmap));
	$root->{files}->{"\0\0bitmap"}->{data} = $bitmap;
    }
}

# Emit one FAT entry. $hold should be a reference to a scalar
# used for the even-numbered (first) part of a FAT12 pair.
# The number should be masked so that the special numbers at the end
# of the number range can be represeented by -9 to -1.
sub emit_fat12($$$) {
    my($out, $num, $hold) = @_;

    $num &= 0xfff;

    if (!defined($$hold)) {
	$$hold = $num;
	return 0;
    }

    $num <<= 12;
    $num |= $$hold;
    undef($$hold);
    return emit($out, pack('vC', $num & 0xffff, $num >> 16));
}
sub emit_fat16($$$) {
    my($out, $num, $hold) = @_;
    return emit($out, pack('v', $num & 0xffff));
}
sub emit_fat32($$$) {
    my($out, $num, $hold) = @_;
    return emit($out, pack('V', $num & 0x0fffffff));
}
sub emit_exfat($$$) {
    my($out, $num, $hold) = @_;
    return emit($out, pack('V', $num & 0xffffffff));
}

sub emit_one_fat($) {
    my($root) = @_;
    my $parm = $root->{parm};
    my $out = $parm->{out};
    my %emit_func = (
	12 => \&emit_fat12,
	16 => \&emit_fat16,
	32 => \&emit_fat32
    );
    my $emit_fat = $parm->{exfat} ? \&emit_exfat : $emit_func{$parm->{fattype}};
    my $hold;
    my $bytes = 0;

    # The two special entries at the start
    $bytes += $emit_fat->($out, $parm->{media} - 256, \$hold);
    $bytes += $emit_fat->($out, -1, \$hold);

    my $clustno = 2;
    foreach my $f (@{$root->{layout}}) {
	next if (!$f->{clustno});		# No cluster chain
	die if ($f->{clustno} != $clustno);	# This would be bad...
	my $nclust = $f->{nclust};
	my $mask = $f->{fatchain} ? -1 : 0;
	while (--$nclust) {
	    # Emit the *next* cluster number
	    $bytes += $emit_fat->($out, (++$clustno) & $mask, \$hold);
	}
	$bytes += $emit_fat->($out, $mask, \$hold); # End of cluster chain
	$clustno++;
    }

    # Flush any partial output
    while (defined($hold)) {
	$bytes += $emit_fat->($out, 0, \$hold);
    }

    $bytes += emit_zero($out, ($parm->{fatsize} << $parm->{secshift}) - $bytes);
    return $bytes;
}

# Maximum chunk size for writezero() and emit_file()
my $BUFSIZ = 1024*1024;

sub emit_to_file($$) {
    my($out,$s) = @_;
    print { $out->{fh} } $s;
}
sub emit_to_hash($$) {
    my($out,$s) = @_;
    $out->{hash}->add($s);
}

sub emit($@) {
    my $out = shift(@_);
    my $bytes = 0;

    foreach my $s (@_) {
	$out->{emitfunc}->($out, $s);
	my $len = length($s);
	$bytes += $len;
	$out->{bytes} += $len;
    }
    return $bytes;
}
sub emit_zero($$) {
    my($out, $count) = @_;
    my $bytes = 0;

    while ($count > $bytes) {
	my $chunk = min($count - $bytes, $BUFSIZ);
	$bytes += emit($out, "\0" x $chunk);
    }
    return $bytes;
}

sub dosdate {
    my($time, $tz) = @_;
    if (!defined($tz)) {
	if ($opt{utc}) {
	    $tz = 0;
	} else {
	    my @lt = localtime($time);
	    my @gt = gmtime($time);
	    $tz = ($lt[0] - $gt[0]) + ($lt[1] - $gt[1]) * 60 +
		($lt[2] - $gt[2]) * 3600;
	    if ($lt[5] < $gt[5] || $lt[4] < $gt[4] || $lt[3] < $gt[3]) {
		$tz -= 86400;
	    } elsif ($lt[5] > $gt[5] || $lt[4] > $gt[4] || $lt[3] > $gt[3]) {
		$tz += 86400;
	    }
	}
    }
    my @lt = gmtime($time+$tz);

    $lt[5] -= 80;		# Convert year to 1980-based
    $lt[4] += 1;		# Convert month to 1-based
    if ($lt[5] < 0) {
	@lt = (0, 0, 0, 1, 1, 0);
    } elsif ($lt[5] > 127) {
	@lt = (59, 59, 23, 31, 12, 127);
    }

    $tz = $tz < 0 ? max($tz+114749, 57600) : min($tz+450, 57599);
    $tz = ($tz/900) + 0x80;

    return [($lt[5] << 25) + ($lt[4] << 21) + ($lt[3] << 16) +
	    ($lt[2] << 11) + ($lt[1] << 5) + ($lt[0] >> 1),
	    ($lt[0] & 1) ? 100 : 0, $tz];
}

sub dir_shortent($$$$$) {
    my($name, $attr, $date, $clustno, $size) = @_;

    $size = 0 if ($attr & ($DIR|$VOL));

    my $datetime = $opt{date} || dosdate($date);
    return pack('A11CCCVvvVvV', $name, $attr, 0, 0,
		$datetime->[0], $datetime->[0] >> 16, $clustno >> 16,
		$datetime->[0], $clustno & 0xffff, $size);
}

sub dir_longent($$) {
    my($longname, $shortent) = @_;

    my $namesum = 0;
    foreach my $nb (unpack('C11', $shortent)) {
	$namesum = (($namesum & 1) << 7) + ($namesum >> 1) + $nb;
	$namesum &= 0xff;
    }

    my $longpos = length($longname);
    my $longpad = $longpos % 26;
    if ($longpad) {
	$longname .= "\0\0";
	$longname .= "\xff" x (24 - $longpad);
	$longpos  = length($longname);
    }
    my $longctr = ($longpos/26) | 0x40;
    my $longent = '';
    while ($longctr) {
	$longpos -= 26;
	$longent .= pack('Ca10CCCa12va4', $longctr,
			 substr($longname, $longpos, 10),
			 0x0f, 0, $namesum,
			 substr($longname, $longpos+10, 12),
			 0,
			 substr($longname, $longpos+22, 4));
	$longctr = ($longctr & 0x3f) - 1;
    }

    return $longent . $shortent;
}

sub exfat_csum($$) {
    my($s,$bits) = @_;
    my $csum = 0;
    my $shift = $bits-1;
    my $mask = (1 << $bits) - 1;
    foreach my $b (unpack('C*', $s)) {
	$csum = ((($csum & 1) << $shift) + ($csum >> 1) + $b) & $mask;
    }
    return $csum;
}

sub emit_direntry_fat($$) {
    my($f, $parm) = @_;

    next if ($f->{type} & $EXFAT);
    my $attr = ($f->{type} & ($DIR|$VOL)) | $f->{flags};
    my $ent = dir_shortent($f->{shortname}, $attr, node_date($f),
			   $f->{clustno}, $f->{size});
    $ent = dir_longent($f->{longname}, $ent);

    return emit($parm->{out}, $ent);
}

sub emit_direntry_exfat($$) {
    my($f, $parm) = @_;
    my $ent;
    my $do_checksum = 1;

    if ($f->{type} == $EXFAT) {
	my $extype = $f->{extype};
	if ($extype == $EXFAT_VOLID) {
	    $ent .= pack('CCva16a10', $extype, 0, $f->{root}->{volid}, '');
	} elsif ($extype == $EXFAT_BITMAP) {
	    $ent .= pack('CC@20VQ<', $extype, 0, $f->{clustno}, $f->{size});
	    $do_checksum = 0;
	} elsif ($extype == $EXFAT_UPCASE) {
	    $ent .= pack('C@4V@20VQ<', $extype, exfat_csum($f->{data},32),
			 $f->{clustno}, $f->{size});
	    $do_checksum = 0;
	} else {
	    die;
	}
    } elsif ($f->{type} == $VOL) {
	$ent = pack('CCa22a8',
		    $EXFAT_VOL,
		    min(length($f->{longname}) >> 1, 11),
		    $f->{longname}, '');
    } else {
	my $longname = $f->{longname};
	my $tz = ((($f->{tz}/900) << 2) & 0x7f) | 0x80;
	my $datetime = $opt{date} || dosdate($f->{date});
	my $size = ($f->{type} & $DIR) ? $f->{paddedsize} : $f->{size};
	$ent = pack('CCvvV3C5a7',
		    $EXFAT_FILE, div_up(length($longname), 30)+1,
		    ($f->{type} & $DIR)|$f->{flags}, 0,
		    ($datetime->[0]) x 3, ($datetime->[1]) x 2,
		    ($datetime->[2]) x 3, '');

	my $allocflags = $f->{size} ? ($f->{fatchain} ? 0x01 : 0x03) : 0x00;

	$ent .= pack('CCCCvvQ<VVQ<',
		     $EXFAT_STREAM, $allocflags, 0, length($longname) >> 1,
		     exfat_csum(upcase_longname($longname),16), 0,
		     $size, 0, $f->{clustno}, $size);

	for (my $p = 0; $p < length($longname); $p += 30) {
	    $ent .= pack('CCa30', $EXFAT_NAME, 0, substr($longname, $p, 30));
	}
    }
    substr($ent,2,0) = pack('v', exfat_csum($ent,16)) if ($do_checksum);

    return emit($parm->{out}, $ent);
}

# Emit the contents of a directory
sub emit_dir {
    my($dir, $parm) = @_;
    my $de;

    my $up = $dir->{up};
    my $bytes = 0;

    if ($parm->{exfat}) {
	foreach my $f (@{$dir->{filelist}}) {
	    $bytes += emit_direntry_exfat($f, $parm);
	}
    } else {
	# Special entries at the beginning
	if (defined($up)) {
	    # Not the root directory, emit . and ..
	    my $dirdate = $dir->{date} = node_date($dir);

	    $de  = dir_shortent('.',  $DIR, $dirdate, $dir->{clustno}, 0);

	    # For some stupid reasons .. is supposed to have 0 in the cluster
	    # pointer when it points to the root directory even on FAT32...
	    my $upptr = defined($up->{up}) ? $up->{clustno} : 0;
	    $de .= dir_shortent('..', $DIR, $dirdate, $upptr,  0);
	    $bytes = emit($parm->{out}, $de);
	}

	# The regular directory entry list, already sorted
	foreach my $f (@{$dir->{filelist}}) {
	    $bytes += emit_direntry_fat($f, $parm);
	}
    }

    die if ($bytes != $dir->{size});
    $bytes += emit_zero($parm->{out}, $dir->{paddedsize} - $bytes);
    return $bytes;
}

# Copy the contents of a file
sub emit_file {
    my($f, $parm) = @_;
    my $bytes = 0;

    return if (!$f->{size});	# Empty file

    my $out = $parm->{out};

    if ($f->{populate}) {
	$bytes = $f->{populate}->($f, $parm, $out);
    } elsif (defined($f->{data})) {
	$bytes = emit($out, $f->{data});
    } elsif (defined($f->{inpath})) {
	my $sizeleft = $f->{size};
	my $in;
	if (open($in, '<', $f->{inpath})) {
	    binmode($in);
	    while ($sizeleft) {
		my $buf;
		my $chunk = read($in, $buf, min($BUFSIZ, $sizeleft));
		last if (!$chunk);
		$bytes += emit($out, $buf);
		$sizeleft -= $chunk;
	    }
	    close($in);
	}
	if ($sizeleft) {
	    print STDERR "%s: %s: %s\n", $opt{outfile}, $f->{inpath}, $!;
	    $err++;
	}
    }

    if ($bytes > $f->{size}) {
	print STDERR "%s: %s: output size overflow\n",
	    $opt{outfile}, $opt{path};
	$err++;
    }
    $bytes += emit_zero($out, $f->{paddedsize} - $bytes);
    return $bytes;
}

# Generate the FAT superblock
sub emit_superblock_fat($$) {
    my($root, $parm) = @_;
    my $fat = $parm->{fattype};

    my $sec32 = $parm->{sectors};
    my $sec16 = ($fat < 32 && $sec32 <= 0xffff) ? $sec32 : 0;

    my $bs = pack('a3A8vCvCvvCvvvVV',
		  $KILLJMP, $opt{creator},
		  $parm->{sector},
		  $parm->{csectors}, $parm->{resv}, $parm->{nfats},
		  $root->{clustno} ? 0 : $root->{paddedsize} >> 5,
		  $sec16, $parm->{media},
		  $fat < 32 ? $parm->{fatsize} : 0,
		  $parm->{chs}->[2], $parm->{chs}->[1],
		  $parm->{hidden}, $sec32);
    my $backupsb = 6;

    if ($fat >= 32) {
	# Weirdly injected in the middle of the FAT12/16 boot sector
	$bs .= pack('VvvVvva12', $parm->{fatsize}, 0, 0x0000,
		    $root->{clustno}, 1, $backupsb, '');
    }

    $bs .= pack('CCCa4A11A8.a*.v', $parm->{drive}, 0, 0x29, $root->{volid},
		$root->{vollbl}->{shortname}, "FAT$fat",
		$KILLOFFS - length($bs), $KILLBOOT,
		510 - length($bs), 0xaa55);

    my $out = $parm->{out};
    my $bytes = emit($out, $bs);

    if ($fat >= 32) {
	my $fsinfo = pack('A4a480A4VVa12V',
			  'RRaA', '', 'rrAa',
			  $parm->{clust} - $parm->{endclust} + 2,
			  $parm->{endclust},
			  '', 0xaa550000);
	$bytes += emit($out, $fsinfo);
	$bytes += emit_zero($out, ($backupsb - 2) << 9);
	$bytes += emit($out, $bs, $fsinfo);
    }
    $bytes += emit_zero($out, ($parm->{resv} << $parm->{secshift}) - $bytes);
    return $bytes;
}

# Generate exFAT superblock
sub emit_superblock_exfat($$) {
    my($root, $parm) = @_;

    # The use of {fssec} (partition size) rather than {sectors}
    # (sectors actually used by the filesystem) here is intentional
    # and required. The checksum is calculated on a string which does
    # not include the VolumeFlags and PercentInUse fields *at all*,
    # as opposed to as zeroes, hence the odd -3 offsets below.
    my $bs = pack('a3A8@64Q<Q<V5a4vC4@507v.',
		  $KILLJMP, 'EXFAT', $parm->{hidden}, $parm->{disk}->{fssec},
		  $parm->{resv}, $parm->{fatsize}, $parm->{heap},
		  $parm->{clust}, $root->{clustno}, $root->{volid},
		  0x0100, $parm->{secshift}, $parm->{cshift},
		  $parm->{nfats}, $parm->{drive}, 0xaa55, $parm->{sector}-3);
    substr($bs, $KILLOFFS-3, length($KILLBOOT)) = $KILLBOOT;
    $bs .= pack('.V', $parm->{sector}-4, 0xaa550000) x 8;
    $bs .= pack('.', 2*$parm->{sector});

    my $checksum = exfat_csum($bs, 32);
    $bs .= pack('V', $checksum) x ($parm->{sector} >> 2);
    # Insert the non-checksummed fields
    substr($bs, 106, 0) = pack('v', 0);
    substr($bs, 112, 0) = pack('C', (($parm->{endclust}-2)*100)/$parm->{clust});
    return emit($parm->{out}, $bs, $bs);
}

sub emit_superblock($$) {
    my($root, $parm) = @_;

    if ($parm->{exfat}) {
	return emit_superblock_exfat($root, $parm);
    } else {
	return emit_superblock_fat($root, $parm);
    }
}

# Create long and short volume labels.
sub create_vol_label($) {
    my($root) = @_;

    my $vol = $opt{volname};
    $vol = 'EFI BOOT' if ($vol eq '' && $opt{efi});
    $vol = decode_utf8($vol);
    $root->{vollbl} = {	'name' => $vol, 'longname' => genlongname($vol),
			'path' => '<vol>', 'up' => $root, 'root' => $root };
    if ($vol ne '') {
	set_node_type($root->{files}->{"\0\0vol"} = $root->{vollbl}, $VOL);
    } else {
	$root->{vollbl}->{shortname} = 'NO NAME'; # By spec
    }
}

# Set disk, partition, and volume identifiers
sub set_identifiers($) {
    my($root) = @_;
    my $parm = $root->{parm};

    $root->{diskid} = parse_guid($opt{diskid});
    $root->{partid} = parse_guid($opt{partid});
    $root->{volid}  = parse_guid($opt{volid});

    if ($opt{verbose} >= 1) {
	my $vol = $root->{vollbl}->{name};
	my $vostr = sprintf('%04X-%04X',reverse unpack('v*', $root->{volid}));
	if ($parm->{part} != $FLAT) {
	    print STDERR "$opt{outfile}: diskid ",
		guid_str($root->{diskid}, $root->{part} == $MBR), "\n";
	    if ($parm->{part} == $GPT) {
		print STDERR "$opt{outfile}: partid ",
		    guid_str($root->{partid}), "\n";
	    }
	}
	printf STDERR "%s: volid  %s\n", $opt{outfile},
	    guid_str($root->{volid}, !$parm->{exfat});
	printf STDERR "%s: label  %s\n", $opt{outfile},
	    $vol eq '' ? 'none' : "\"$vol\"";
    }
}

sub create_layout($) {
    my($root) = @_;

    # Compute a suitable cluster shift.
    my $parm = undef;
    if ($opt{exfat} < 2 && !$root->{largefile}) {
	for (my $cs = 0; $cs < 7; $cs++) {
	    my $thisparm = get_format_params($root, $cs, 0);
	    next unless (defined($thisparm));
	    if ($opt{verbose} >= 3) {
		printf STDERR "trial: FAT%d, clust %6d: sectors %8d, clusters %8d\n",
		    $thisparm->{fattype}, 1 << $cs, $thisparm->{sectors},
		    $thisparm->{clust};
	    }
	    if (!defined($parm) || ($thisparm->{sectors} < $parm->{sectors})) {
		$parm = $thisparm;
	    }
	    # Avoid 128-sector clusters unless we really need them
	    last if (defined($parm) && $cs >= 6);
	}
    }
    if ($opt{exfat}) {
	for (my $cs = 0; $cs < 18; $cs++) {
	    my $thisparm = get_format_params($root, $cs, 1);
	    next unless (defined($thisparm));
	    if ($opt{verbose} >= 3) {
		printf STDERR "trial: exFAT, clust %6d: sectors %8d, clusters %8d\n",
		    1 << $cs, $thisparm->{sectors}, $thisparm->{clust};
	    }
	    if (!defined($parm) || ($thisparm->{sectors} < $parm->{sectors})) {
		$parm = $thisparm;
	    }
	}
    }

    die "$opt{outfile}: unable to create a valid filesystem (too large?)\n"
	unless (defined($parm));
    $root->{parm} = $parm;

    # Cluster bytes
    my $csectors = $parm->{csectors};

    # Block alignment in sectors
    my $align     = $parm->{partalign};
    my $gptsize   = div_up(max($opt{gptent},
			       $opt{strict}->{gptsize} ? 128 : 1),
			   $parm->{sector} >> 7);
    my $startsec  = 0;
    my $endsec    = 0;
    my $sectors   = $parm->{sectors};
    $parm->{part} = $opt{part};
    if ($parm->{part} == $ANYPART) {
	$parm->{part} =
	    $opt{efi} || ($sectors+$align) > 0xffffffff ? $GPT : $MBR;
    }
    if ($parm->{part} == $GPT) {
	$startsec   = $gptsize + 2; # +2 for PMBR, header
	if ($opt{backup_gpt}) {
	    $endsec   = $gptsize + 1;
	    $opt{pad} = 1;	# Padding to end of "disk" required
	}
    } elsif ($parm->{part} == $MBR) {
	$startsec = 1;
    } else {
	$parm->{part} = $FLAT;
    }
    # Initial partition table + alignment + space for backup gpt if necessary
    $startsec  = align_up($startsec, $align);
    $sectors   = align_up($sectors + $startsec + $endsec, $align);
    $endsec    = $sectors - $endsec;
    my $fssec  = $endsec  - $startsec;
    $parm->{disk} = {
	'align'    => $align,
	'startsec' => $startsec,
	'endsec'   => $endsec,
	'fssec'    => $fssec,
	'gptsize'  => $gptsize,
	'sectors'  => $sectors
    };

    # Update with the actually generated layout
    finalize_format_params($root, $startsec, $fssec);
    if ($opt{verbose} >= 2) {
	printf STDERR "%s: %s, %d sectors, %d clusters * %d bytes\n".
	    "%s: filesystem @ 0x%08x, data @ 0x%08x\n",
	    $opt{outfile}, $parm->{exfat} ? 'exFAT' : $parm->{fattype},
	    $fssec, $parm->{clust}, $parm->{cbytes},
	    $opt{outfile}, $startsec << $parm->{secshift}, $parm->{dataoffset};
    }
    assign_clusters($root);
    print $startsec << $parm->{secshift}, "\n" if ($opt{print_offset});
}

sub emit_image($$) {
    my($root, $out) = @_;
    my $parm = $root->{parm};
    my $disk = $parm->{disk};

    $out->{bytes} = 0;
    $parm->{out}  = $out;

    # Create the partition table
    my @part; # MBR, GPT header, GPT array, backup GPT header
    if ($parm->{part} == $GPT) {
	@part = gen_gpt($disk->{sectors}, $disk->{startsec},
			$disk->{fssec}, $parm->{gpttype},
			$disk->{gptsize}, $root->{vollbl}->{name},
			$root->{diskid}, $root->{partid}, $parm->{chs});
    } elsif ($parm->{part} == $MBR) {
	$part[0] = gen_mbr($disk->{startsec}, $disk->{fssec},
			   $parm->{mbrtype}, $parm->{chs},
			   $parm->{active}, $root->{diskid});
    }

    my $bytes = emit($out, @part[0 ... 2]);
    $bytes += emit_zero($out,
			($disk->{startsec} << $parm->{secshift}) - $bytes);

    # The boot sector/superblock
    $bytes += emit_superblock($root, $parm);

    # The FATs
    for (my $i = 0; $i < $parm->{nfats}; $i++) {
	$bytes += emit_one_fat($root);
    }

    # Special entries, root directory, subdirectories, files
    $bytes += populate_with($root, $parm, $EXFAT, \&emit_file);
    $bytes += emit_dir($root, $parm);
    $bytes += populate_with($root, $parm, $DIR,   \&emit_dir);
    $bytes += populate_with($root, $parm, $FILE,  \&emit_file);

    # Finally, pad the image and output the backup GPT if there is one
    # This does not need to be included in the final hash, so...
    if ($opt{pad} && $out->{fh}) {
	$bytes += emit_zero($out,
			    ($disk->{endsec} << $parm->{secshift}) - $bytes);
	$bytes += emit($out, @part[2 ... 3]) if (defined($part[3]));
	die unless ($bytes == ($disk->{sectors} << $parm->{secshift}));
    }
    return $bytes;
}

sub usage(;$) {
    my($err) = @_;
    my $out = $err ? \*STDERR : \*STDOUT;
    print $out <<EOF;
$myname $myversion
Usage: $myname [options] [-o] outfile [[[destination]:]:]input_path...

Create and populate a FAT filesystem image. By default it tries to
minimize the image as much as possible, leaving no free space and
minimizing all data structures to the smallest possible.

Inputs can be files or directories. By default, if the input is a
directory, its input_path will be discarded entirely and the contents
of that directory will be added to the root directory; if the input is
a file, the filename will be used as its name and the file is added to
the root directory.

Pathnames that are invalid in FAT are converted unless
--strict=filename is in use.

The prefix \"destination::\" specifies that an input should be located
at a certain destination path in the generated image. If the
destination ends in / and the input is a file, the file will be
located in the given directory.

A prefix of \":\" is equivalent to no prefix at all. This is
recommended if input_path may contain \"::\".

A prefix of \"::\" alone as that the entire input_path should be used
as the destination name.

Most options can be negated by prefixing their long forms with --no-.

Options:
-o, --output             Specify the output file (- for standard output)
    --flat, --no-part    Do not create a partition table
    --part               Create a partion table, either MBR or GPT [default]
    --mbr                Create an MBR partition table
    --gpt[=<partitions>] Create a GPT partition table [default with --efi]
-e ,--efi, --uefi        Mark the partition as an EFI system partition
-b, --boot, --active     Mark the partition as legacy BIOS bootable
-L, --label=<name>	 Set the filesystem volume name
-H, --heads=#            Define heads per cylinder for legacy geometry [255]
-S, --secpertrack=#      Define sectors per track legacy geometry [63]
-v, --verbose[=#]        Set message verbosity
-r, --reserve=<files>,<bytes>,<rootfiles>,<namelen>
                         Specify additional space to reserve in the filesystem
             files:      Specify the minimum number of (small) new files [0]
             bytes:      Specify additional bytes of free space [0]
             rootfiles:  Indicate how may of <files> may be in / [0]
             namelen:    Assumed maximum filename length for new files [64]
-m, --media              Settings suitable for writing to physical media
    --strict             Strictly adhere to recommended filesystem parameters
    --strict=...         Fine grained control of strictness flags
    --strict=help        Show detailed help about the --strict option
-x, --exfat              Generate an exFAT filesystem if advantageous
-X, --exfat=force        Unconditionally generate an exFAT filesystem
-a, --align              Align data structures to multiples of the cluster size
    --no-pad             Do not write out empty sectors at the end of the image
    --no-backup-gpt      Omit the end-of-image backup GPT partition table
-R, --random             Use random IDs by default instead of a repeatable hash
    --volid=<id>         Set the volume ID to <id> (exFAT: GUID, FAT: 8 hex)
    --partid=<id>        Set the partition ID to <id> (GPT only: GUID)
    --diskid=<id>        Set the disk ID to <id> (GPT: GUID, MBR: 8 hex)
-U, --utc                Create time stamps in UTC, not local time
-D, --date=<date>        Force the time stamp of all files to <date>
-C, --creator=<name>     Set the filesystem creator name to <name> [$mycreator]
    --print-offset       Print filesystem offset in the image after completion
-V, --version            Display the version information
-h, --help               Display this help text
EOF
    exit($err);
}
sub strict_help($) {
    my($err) = @_;
    my $out = $err ? \*STDERR : \*STDOUT;

    print $out <<EOF;
--strict option syntax: --strict=[+-]flag[,flag...]

If preceeded by +, add to existing flags.
If preceeded by -, remove from existing flags.

--strict without any argument is equivalent to --strict=all, which sets
all flags.

For forward compatibility reasons, unknown flags are ignored unless
--strict=flags is specified.

Available flags are:
EOF

    map { printf $out "    --strict=%-10s %s\n", $_, $strict_help{$_} }
	sort(keys(%strict_help));
    exit($err);
}

sub get_arg($$;$) {
    my($args,$os,$truth) = @_;

    return undef if (defined($truth) && !$truth);
    return $1 if ($os =~ /^-.*?=(.*)$/);	# = separated argument
    return undef if ($truth < 0);		# Missing optional argument
    my $arg = shift(@$args);
    die "$myname: option -$os requires an argument\n" if (!defined($arg));
    return $arg;
}
sub get_arg_uints($$;$$) {
    my($args,$os,$truth,$cmax) = @_;

    my $arg = get_arg($args, $os, $truth);
    return () unless ($arg ne '');

    my @ns;
    foreach my $a (split(/,/, $arg)) {
	if ($a =~ /^\s*(0x[0-9a-f]|[0-9]+)\s*([kmgtpe])?\s*$/i) {
	    my $suf = lc($2);
	    my $n = $1;
	    $n = hex($n) if ($n =~ /^0x/i);
	    push(@ns, $n << (10 * index(' kmgtpe', $suf)));
	} else {
	    undef @ns;
	    last;
	}
    }
    if (!@ns || scalar(@ns) > max($cmax,1)) {
	die "$myname: invalid argument to -$os: $arg\n";
    }
    return \@ns;
}
sub get_arg_uint($$;$) {
    my($args,$os,$truth) = @_;
    my $ns = get_arg_uints($args, $os, $truth);
    return $ns ? $ns->[0] : undef;
}

sub do_options($@) {
    my $args = shift(@_);	# Argument list pointer

    foreach my $optn (@_) {
	my $os = $optn;
	my $truth = ($os !~ s/^-no-?([^=]+)$/-$1/);

	if ($os =~ /^(?:o|-output)$/) {
	    $opt{output} = $truth ? get_arg($args, $os) : File::Spec->devnull();
	} elsif ($os =~ /^(?:e|-efi|-uefi)$/) {
	    $opt{efi} = $truth;
	} elsif ($os =~ /^(?:g|-gpt)(=(\d+))$/) {
	    if ($truth) {
		$opt{part} = $GPT;
		$opt{gptent} = $2+0 if ($1);
	    } else {
		$opt{part} = $FLAT unless ($opt{part} == $MBR);
	    }
	} elsif ($os =~ /^(?:b|-active|-(?:legacy-?)?boot(?:able)?)$/) {
	    $opt{active} = $truth;
	} elsif ($os =~ /^(?:m|-mbr)$/) {
	    if ($truth) {
		$opt{part} = $MBR;
	    } else {
		$opt{part} = $FLAT unless ($opt{part} == $GPT);
	    }
	} elsif ($os =~ /^(?:f|-flat)$/) {
	    if ($truth) {
		$opt{part} = $FLAT;
	    } else {
		$opt{part} = $ANYPART if ($opt{part} == $FLAT);
	    }
	} elsif ($os =~ /^(?:p|-part)$/) {
	    if ($truth) {
		$opt{part} = $ANYPART unless ($opt{part} > $FLAT);
	    } else {
		$opt{part} = $FLAT;
	    }
	} elsif ($os =~ /^(?:r|-reserve)(?:=.*)?$/) {
	    $opt{reserve} = get_arg_uints($args,$os,$truth,4);
	} elsif ($os =~ /^(?:R|-random)$/) {
	    $opt{random} = $truth;
	} elsif ($os =~ /^(?:v|-verbose)(=(\d+))?$/) {
	    $opt{verbose} = !$truth ? 0 : $1 ? $2+0 : max($opt{verbose}+1, 1);
	} elsif ($os =~ /^(?:q|-quiet)$/) {
	    $opt{verbose} = $truth ? -1 : max($opt{verbose}, 0);
	} elsif ($os =~ /^(?:H|-heads)(?:=.*)?$/) {
	    $opt{h} = get_arg_uint($args, $os,$truth);
	} elsif ($os =~ /^(?:S|-secpertrack)(?:=.*)?$/) {
	    $opt{s} = get_arg_uint($args, $os,$truth);
	} elsif ($os =~ /^(?:L|n|-label|-name|-volume(?:-?name)?)(?:=.*)?$/) {
	    $opt{volname} = get_arg($args,$os,$truth) . '';
	} elsif ($os =~ /^(?:s|-strict)(=([+-]?)(.*))?$/) {
	    my $flags = $3;
	    my $sense = $2;
	    strict_help(0) if ($flags eq 'help');
	    $truth = 0 if ($sense eq '-' || $flags =~ /^(?:no|none|off|0)$/);
	    $opt{strict} = {} if (!$sense); # Clear all flags
	    if ($flags =~ /^(?:no|none|off|0|yes|all|1)$/) {
		$flags = join(',', keys(%strict_help));
	    }
	    my $found_unknown;
	    foreach my $flag (split(/,/, $flags)) {
		$found_unknown = $flag if (!$strict_help{$flag});
		$opt{strict}->{$flag} = $truth;
	    }
	    if (defined($found_unknown) && $truth && $opt{strict}->{flags}) {
		print STDERR "$myname: invalid --strict flag: $found_unknown\n";
		strict_help(1);
	    }
	} elsif ($os =~ /^(?:a|-align)$/) {
	    $opt{align} = $truth;
	} elsif ($os eq '-pad') {
	    $opt{pad} = $truth;
	} elsif ($os =~ /^(?:([Xx])|-exfat)(?:=(.*))?$/) {
	    my($xx,$oo) = ($1,$2);
	    $truth = 0 if ($oo =~ /^(?:no|never|off|0)$/);
	    $truth = 2 if ($xx eq 'X' || $oo =~ /^(?:force|always|1)$/);
	    $opt{exfat} = $truth;
	} elsif ($os =~ /^(?:m|-media)$/) {
	    $opt{media} = $truth;
	} elsif ($os =~ /^-diskid(?:=.*)?$/) {
	    $opt{diskid} = get_arg($args,$os,$truth);
	} elsif ($os =~ /^-partid(?:=.*)?$/) {
	    $opt{partid} = get_arg($args,$os,$truth);
	} elsif ($os =~ /^-volid(?:=.*)?$/) {
	    $opt{volid} = get_arg($args,$os,$truth);
	} elsif ($os =~ /^(?:C|-creator)(?:=.*)?$/) {
	    $opt{creator} = get_arg($args,$os,$truth);
	} elsif ($os =~ /^(?:D|-date(?:time)?)(?:=.*)?$/) {
	    $opt{date} = get_arg($args,$os,$truth);
	} elsif ($os =~ /^(?:U|-utc|-gmt)$/) {
	    $opt{utc} = $truth;
	} elsif ($os =~ /^-print-offset$/) {
	    $opt{print_offset} = $truth;
	} elsif ($os =~ /^(?:h|-help)$/) {
	    usage(0);
	} elsif ($os =~ /^(?:V|-version)$/) {
	    print "$myname $myversion\n";
	    exit(0);
	} else {
	    die "$myname: unknown option: -$os (--help for help)\n";
	}
    }
}

sub parse_options(@_) {
    my(@args) = @_;
    my $output;

    while (defined(my $arg = $args[0])) {
	last if ($arg !~ /^-((-?).*)$/);
	shift(@args);
	if ($2) {
	    last if ($1 eq '-');
	    do_options(\@args, $1);
	} else {
	    do_options(\@args, split(//, $1));
	}
    }
    $opt{outfile} = shift(@args) unless (defined($opt{outfile}));

    return @args;		# Input files
}

sub parse_tz($) {
    my($tz) = @_;
    return undef
	unless($tz =~ /^([+-]?)(\d{1,2})[:\.]?(\d{,2})[:\.]?(\d{,2})(?:\.\d*)?$/);
    my $sec = $4 + 60 * ($3 + 60*$2);
    return ($1 eq '-') ? -$sec : $sec;
}
sub mystr2time($$) {
    my($date, $utc) = @_;
    my($time,$tz);
    if ($date =~ /^\@?0?x([0-9a-f]+)([+-]\d+)?$/i) {
	$time = hex($1);
	$tz = parse_tz($2) || 0;
    } elsif ($date =~ /^\@?(\d+(?:\.d*)?)([+-]\d+)?$/) {
	$time = $1+0;
	$tz = parse_tz($2) || 0;
    } else {
	my $tzstr = $utc ? '+0000' : undef;
	if (!defined($time = str2time($date, $tzstr))) {
	    die "$myname: invalid date: $date\n";
	}
	my @lt = strptime($date, $tzstr);
	$tz = $lt[6];
    }
    return ($time, $tz);
}

my(@inputs) = parse_options(@ARGV);
if (!@inputs) {
    die "$myname: no input files (see --help for usage)\n";
}

# Hopefully correct...?!
binmode(STDERR, ':encoding(UTF-8)');

# Postprocessing of some options
unless (defined($opt{creator})) {
    $opt{creator} = $opt{strict}->{creator} ? 'WINNT4.1' : $mycreator;
}
if ($opt{media}) {
    map { $opt{strict}->{$_} = 1 unless (defined($opt{strict}->{$_})) }
	qw(volsize upcase fats mbrspace bootspace rootdir gptsize);
    $opt{align}  = 1 unless (defined($opt{align}));
}
$opt{date} = dosdate(mystr2time($opt{date}, $opt{utc})) if ($opt{date} ne '');

if ($opt{efi} && $opt{exfat} && $opt{verbose} >= 0) {
    print STDERR "$opt{outfile}: WARNING: EFI and exFAT are not compatible\n";
}

my $root = read_inputs(@inputs);
exit(1) if ($err);

create_vol_label($root);
prep_directories($root);
create_layout($root);

if (!$opt{random}) {
    my %optsave = %opt;
    $opt{verbose} = 0;
    $opt{print_offset} = 0;
    $pseudo_random_buf = "\xee" x 64;
    set_identifiers($root);
    my %ho = ('hash' => Digest::SHA->new(512), 'emitfunc' => \&emit_to_hash);
    emit_image($root, \%ho);

    %opt = %optsave;
    $pseudo_random_buf = $ho{hash}->digest;
}

set_identifiers($root);	# Update identifiers

my %oo = ('emitfunc' => \&emit_to_file);
END {
    if (defined($oo{filename}) && $oo{fh}) {
	close($oo{fh});
	unlink($oo{filename});
    }
}

if ($opt{outfile} eq '-') {
    $oo{fh} = \*STDOUT;
} else {
    $oo{filename} = $opt{outfile};
    open($oo{fh}, '>', $oo{filename}) or die "$opt{outfile}: $!\n";
}
emit_image($root, \%oo);

if (defined($oo{filename})) {
    close($oo{fh});
    undef($oo{filename});	# Don't delete
}

exit(0);
